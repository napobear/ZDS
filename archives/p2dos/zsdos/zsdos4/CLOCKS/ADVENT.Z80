	TITLE	"Driver for Advent-Kaypro Clock - (KPRO-ADVENT)"
	SUBTTL	"Description of Clock Module"
;--------------------------------------------------------------------
; Translated for Z80ASM and ZSDOS 06/11/88 from Ver of 7/28/85
; Ver 0.7 - Corrected exit conditions.		15 Oct 88,  H.F.Bower
;--------------------------------------------------------------------

VERS	EQU	07
	.Z80
	NAME ADV

	MACLIB	CLOCK.LIB

	COMMON	/_CLKID_/

DESCST:	DEFW	0		; Thisyr

CLKNAM:	DEFB	'Advent                  '	; Exactly 24 chars
	DEFB	VERS/10+'0','.',VERS MOD 10 + '0',0

DESCR:	DEFB	'   Kaypro - Advent Turbo Clock - MM58167',CR,LF
	DEFB	'   (year held in $1 seconds latch) (Advent Products)',0


	PAGE
	SUBTTL	"Configurable Hardware Clock Parameters"

	COMMON	/_PARM_/

PARBAS:	DEFW	NPARAMS		; # of parameters
	DEFW	STRS

NP0:
XPORT	EQU	$+1
	DEFB	BYTE
	DEFW	PORT

NPARAMS	EQU	($-NP0)/3

STRS:	DEFB	'Clock Data Port ',0


	PAGE
	SUBTTL	"Clock Code - Advent Kaypro Clock"

PORT	EQU	090H		; Port for clock

; register numbers in 58157

YEAR	EQU	9		; Use the subsecond latch
SECONDS	EQU	2

REGMSK	EQU	01001000B	; Yr,thsec,mon,day,daywk,hr,min,sec
				; 9  8	   7   6   5	 4  3	2

	CSEG
;-------------------------------------------------------------
;	    Z S D O S    C L O C K    H E A D E R
;-------------------------------------------------------------
; Enter: HL points to a 6-byte buffer to Get/Set Time
; Exit :  A=1 on Success, A=FFH if error
;	 HL points to last char in buffer

;== Commented out til Set code written
;==	JP	GETTIM		; Jump to Read Clock
;==	JP	WRCLK		; Jump to Set Clock

;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
;		R e a d    T h e    C l o c k
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

GETTIM:	PUSH	HL
RETRY:	POP	HL
	PUSH	HL

;read seconds before starting -(to guard against rollover)

YPORT1	EQU	$+1
	LD	BC,SECONDS*256+PORT
	IN	A,(C)

;set up for block read

	LD	D,REGMSK
YPORT2	EQU	$+1
	LD	BC,YEAR*256+PORT

LOOP:	SCF
	RL	D
	JR	C,$$SKIP
	LD	E,(HL)
	INI			; Get reading
	INC	B
$$SKIP:	DJNZ	LOOP

;now test for overflow

DONE:	DEC	HL
	CP	(HL)
	LD	A,01		;== 0.7
	POP	BC
	RET	Z
	PUSH	BC
	LD	(HL),E		; Fix sec for retry
	JR 	RETRY		; Seconds rollover

;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
;		S e t    T h e    C l o c k
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
;== Commented out til written
;==WRCLK:
;==	RET


	PAGE
	SUBTTL	"Run-Time Configuration of Ports and Masks"

; This code installs configurable items into the clock module
; Enter with DE pointing to the physical address of the relocatable module

	COMMON	/_POST_/

	SETBYT	XPORT,YPORT1
	SETBYT	,YPORT2
	RET


	PAGE
	SUBTTL "Pre-Execution Clock Checks"

; This module is executed just prior toinstalling the module to insure
; that a valid clock is present
; Enter: DE contains address of the relocated Clock routine

	COMMON	/_PRE_/

	INCLUDE	PRECLOCK.LIB

	END
