	TITLE	"Big Board II Heartbeat Clock - (BIG-BD-II)"
	SUBTTL	"Description of Clock Module"
;========================================================
; Translated for Z80ASM/ZSDOS 06/11/88 from ver of 7/6/85
; Vers 0.5 current as of 16 Oct 88
;========================================================

VERS	EQU	05
	.Z80
	NAME BBII

	MACLIB	CLOCK.LIB

	COMMON	/_CLKID_/

DESCST:	DEFW	0		; Year

CLKNAM:	DEFB	'Big Board II            '	; Exactly 24 chars
	DEFB	VERS/10+'0','.',VERS MOD 10 + '0',0

DESCR:	DEFB	'   Big Board II 60 Hz Heartbeat Clock',CR,LF
	DEFB	'   (Neil Maron)',0

	  IF  [$-DESCST] > 256
	OVER2
	  ENDIF


	PAGE
	SUBTTL	"Configurable Clock Hardware Parameters"

	COMMON	/_PARM_/

PARBAS:	DEFW	NPARAMS		; # of parameters
	DEFW	STRS

NP0:
XCLKAD	EQU	$+1
	DEFB	WORD
	DEFW	CLKADD

NPARAMS	EQU	($-NP0)/3

STRS:	DEFB	'Seconds RAM Address ',0


	PAGE
	SUBTTL	"Clock Code - Big Board II"

CLKADD	EQU	0FF7CH
YEAR	EQU	5
SECONDS	EQU	0

	CSEG
;-----------------------------------------------------------
;	    Z S D O S    C L O C K    H E A D E R
;-----------------------------------------------------------
; Enter: HL points to a 6-byte buffer to Get/Set Time
; Exit :  A=1 on Success, A=FFH if error
;	 HL points to last char in buffer

;== Commented out til Set code written
;==	JP	GETTIM		; Jump to Read Clock
;==	JP	WRCLK		; Jump to Set Clock

;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
;		R e a d    T h e    C l o c k
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

YCLKAD	EQU	$+1
GETTIM:	LD	DE,CLKADD+YEAR
	LD	B,5
	DI			; Prevent rollover
LP:	LD	A,(DE)
	LD	(HL),A
	INC	HL
	DEC	DE
	DJNZ	LP
	LD	A,(DE)		; Get seconds
	EI
	LD	E,(HL)		; Return buffer 'seconds' in e
	LD	(HL),A		; Install seconds reading
	LD	A,01		; Set Ok return status
	RET

;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
;		S e t    T h e    C l o c k
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
;== Commented out til written
;==WRCLK:
;==	RET


	PAGE
	SUBTTL	"Run-Time Configuration of Ports and Masks"

; This code installs configurable items into the clock module
; Enter with DE pointing to the physical address of the relocatable module

	COMMON	/_POST_/

	LD	HL,(XCLKAD)
	LD	BC,YEAR
	ADD	HL,BC
	EX	DE,HL		; Put word in DE
	LD	BC,YCLKAD
	ADD	HL,BC		; Calculate destination addr
	LD	(HL),E
	INC	HL
	LD	(HL),D
	RET


	PAGE
	SUBTTL	"Pre-Execution Clock Checks"

; This module is executed just prior to installing the module to insure
; that a valid clock is present

	COMMON	/_PRE_/

	INCLUDE	PRECLOCK.LIB

	END
