	TITLE	"Electralogics MFIO - S100 - (ELECTR-MFIO)"
	SUBTTL	"Description of Clock Module"
;=================================================================
; Author of ASM version: 6/11/85
;  Don McClimans
;  Inti Electronics Corp$
;  108 Boardman St$
;  Rochester, NY 14607
;  716-271-6280
;
; The MFIO board has an I/O Port "bank switching" scheme.  Only 8
; registers are available on the board at any one time.  To switch
; between them, you output a select byte to the select port.
;=================================================================
; Translated for Z80ASM/ZSDOS 06/11/88 from ver of 7/6/85
; Ver 0.1 current as of 17 Oct 88
;=================================================================

VERS	EQU	01
	.Z80
	NAME ELMFIO

	MACLIB	CLOCK.LIB

	COMMON	/_CLKID/

DESCST:	DEFW	0		; Thisyr

CLKNAM:	DEFB	'Electralogics MFIO      '	; Exactly 24 chars
	DEFB	VERS/10+'0','.',VERS MOD 10 + '0',0

DESCR:	DEFB	'   Electralogics MFIO - S100 - MM58167',CR,LF
	DEFB	'   8080 (year in $1 & $01 seconds latch)',0

	  IF  [$-DESCST] > 256
	OVER2
	  ENDIF


	PAGE
	SUBTTL	"Configurable Clock Hardware Parameters"

	COMMON	/_PARM_/

PARBAS:	DEFW	0		; Dummy parameters
	DEFW	0


	PAGE
	SUBTTL	"Clock Code - Electrologics MFIO Clock"

SELECTP	EQU	78H		; Selects block of 8 ports
CLKSEL	EQU	0CH		; Selects the clock registers
ALRMSEL	EQU	0DH		; Selects the alarm registers
STATSEL	EQU	0EH		; Selects the status/cmnd registers

; After a set of 8 registers is selected, the one desired can be input
; from and output to at 8 port addresses starting with BASEPORT$

BASEPORT EQU	70H

; When the clock is selected, the following registers are used

SECONDS	EQU	BASEPORT+2
MINUTES	EQU	BASEPORT+3
HOURS	EQU	BASEPORT+4
WEEKDAY	EQU	BASEPORT+5
DAY	EQU	BASEPORT+6
MONTH	EQU	BASEPORT+7

; When the alarm is selected, the following register is used
; This is nominally the hundreths and tenths of a second alarm time.
; However, we do not enable this interrupt, and store the current year
; there;

YEAR	EQU	BASEPORT+1

; When the cmnd/status is selected, the following register is used

ROLLOVER EQU	BASEPORT+4

; This is the rollover bit in the rollover register

ROLLBIT	EQU	01H

	CSEG
;-----------------------------------------------------------
;	    Z S D O S    C L O C K    H E A D E R
;-----------------------------------------------------------
; Enter: HL points to a 6-byte buffer to Get/Set Time
; Exit :  A=1 on Success, A=FFH if error
;	 HL points to last char in buffer

;== Commented out til Set code written
;==	JP	GETTIM		; Jump to Read Clock
;==	JP	WRCLK		; Jump to Set Clock

;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
;		R e a d    T h e    C l o c k
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

GETTIM:	LD	A,ALRMSEL	; Get the year
	OUT	(SELECTP),A
	IN	A,(YEAR)
	LD	(HL),A		; And store in buffer
	INC	HL		; Increment buffer pointer

; Get the date and time

	LD	A,CLKSEL	; Select the clock registers
	OUT	(SELECTP),A

	IN	A,(MONTH)
	LD	(HL),A
	INC	HL
	IN	A,(DAY)
	LD	(HL),A
	INC	HL
	IN	A,(HOURS)
	LD	(HL),A
	INC	HL
	IN	A,(MINUTES)
	LD	(HL),A
	INC	HL
	IN	A,(SECONDS)
	LD	E,(HL)		; Preserve old value
	LD	(HL),A

;now check for rollover while reading

	LD	A,STATSEL
	OUT	(SELECTP),A
	IN	A,(ROLLOVER)
	AND	ROLLBIT
	LD	A,01		;== Set Ok return in case it's good
	RET	Z

; Retry if rollover bit set

RETRY:	LD	(HL),E		; Restore old seconds
	LD	DE,-5
	ADD	HL,DE
	JR	GETTIM

;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
;		S e t    T h e    C l o c k
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
;== Commented out til written
;==WRCLK:
;==	RET



	PAGE
	SUBTTL	"Run-Time Configuration of Ports and Masks"

; This code installs configurable items into the clock module
; Enter with DE pointing to the physical address of the relocatable module

	COMMON	/_POST_/

	RET			; Just return if arrive here

	DEFB	0,0,0		; Filler

	PAGE
	SUBTTL	"Pre-Execution Clock Checks"

; This module is executed just prior to installing the module to insure
; that a valid clock is present

	COMMON	/_PRE_/

	INCLUDE	PRECLOCK.LIB

	END
