	TITLE	"DateStamper (tm) Stamp Module for ZSDOS"
;***************************************************************************
;  Z D S								   *
;--------------------------------------------------------------------------*
; Copyright (C) 1988  by Cameron W. Cotrill and Harold F. Bower		   *
;--------------------------------------------------------------------------*
; This module replaces DateStamper V2.30 in ZSDOS systems.  It provides	   *
; support for ZSDOS Functions 102 thru 105.  To simplify the tie-ins with  *
; ZSDOS, this module is "Dirty" - It assumes addresses inside of ZSDOS.    *
;									   *
; The loader for this routine needs to place the address of ZSDOS (Page    *
; Boundary) in the offset entry in JTABLE.  All ZSDOS addresses are dynami-*
; cally calculated from this address.  BIOS is assumed to be 0E00H above   *
; ZSDOS, but this may be changed by using the EQUate.			   *
;									   *
; This program may be used only by licensed Plu*Perfect DateStamper Users! *
;	Agreement pending with Plu*Perfect Systems			   *
;									   *
; The author wishes to acknowledge the numerous suggestions and helpful	   *
; hints from Bridger Mitchell of Plu*Perfect Systems.			   *
;									   *
; Revision:								   *
;	1.0 - Initial Release						   *
;***************************************************************************

VER	EQU	11		; Release Version

FALSE	EQU	0
TRUE	EQU	NOT FALSE

	MACLIB	RSXFLAG		; Get definition of RSX equate flag
				; TRUE=Assemble as RSX, FALSE=Assemble hi-mem
				;== ALSO GET VERSION ADDRESS EQUATES
DOSID	EQU	'S'
DOSVER	EQU	11H
	PAGE
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;  S T A R T    O F    R E L O C A T A B L E    C O D E
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

BEGIN:				; Label for ending space calculations

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
; RSX Install Routine - Used once, then no longer needed.
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

	  IF	  RSX
INSTALL: PUSH	BC
	PUSH	DE		; Save params for others
	LD	A,D
	OR	E		; Is there an Environment?
	JR	Z,INST00	; ..jump if none 'cause can't be NZCOM
	LD	HL,8
	ADD	HL,DE		; Point to ENV type
	BIT	7,(HL)		; Is it an extended Environment?
	JR	Z,INST00	; ..jump if Not
	LD	HL,42H		; Point to DOS addr in Extended Env
	ADD	HL,DE
	LD	A,(HL)		; ..and get it to HL
	INC	HL
	LD	H,(HL)
	LD	L,A
	JR	INST01		; DOS address now in HL

INST00:	LD	C,0		; Insure BIOS is even page
	LD	HL,-0E00H	; Subtract DOS size from BIOS
	ADD	HL,BC		; DOS starting addr now in HL
INST01:	LD	(OFFSET),HL	; Let LOADER know where things are
	PUSH	HL
	LD	DE,6		; Offset from DOS base page to entry
	ADD	HL,DE
	LD	C,48		; Get Version command
	CALL	CALLHL		; ..and do it
	CP	DOSVER		; Is it the correct version?
	JR	NZ,INST02	; ..jump if not
	LD	A,H		; Get type flag byte
	CP	DOSID		; Is it our type?
INST02:	POP	DE		; Restore DOS base addr
	JR	NZ,INSNXT	; Skip install if wrong DOS
	LD	HL,LOADED	; Are we loaded already?
	LD	A,(HL)
	AND	A
	JR	NZ,INSNXT	; If we are loaded already
	DEC	(HL)		; Else flag loaded
	CALL	INSRSX		; Set up header and tie in
	LD	HL,16H		; Offset in DOS to table
	ADD	HL,DE
	EX	DE,HL		; Put in DE
	LD	HL,JRWCLK	; Where to copy from
	LD	BC,12
	LDIR			; Move the Stamping and clock vectors
	EX	DE,HL		; Put DOS table addr in HL
	INC	HL
	INC	HL		; Point to DOS Remove Storage
	LD	DE,MODRMV	; Set module Remove Vector
	LD	(HL),E		; ..and save in DOS table
	INC	HL
	LD	(HL),D
	CALL	INSTA3		; Install patches into DOS
INSNXT:	POP	DE
	POP	BC
	RET

INSRSX:	DI			; Don't interrupt during this
	LD	HL,MODWARM	; Get address of module warm boot
	LD	(MODWB+1),HL	; Place module boot address
	LD	HL,(0006H)	; Get top of memory
	LD	(JTABLE+1),HL
	LD	HL,JTABLE
	LD	(RSXPTA),HL	; Set protect address
	LD	(0006H),HL
	LD	HL,(0001H)
	LD	(WBBIOS),HL	; BIOS Warm Boot target
	INC	HL
	LD	C,(HL)
	INC	HL
	LD	B,(HL)		; Get where WBOOT is pointing
	PUSH	HL
	LD	HL,WBNEXT
	LD	(HL),0C3H	; JP Opcode
	INC	HL
	LD	(HL),C
	INC	HL
	LD	(HL),B		; Place jump to next WBOOT routine
	POP	HL
	LD	BC,MODWB
	LD	(HL),B
	DEC	HL
	LD	(HL),C		; Install ourself into BIOS warm boot
	EI			; Ok, we're tied in - make my day
	RET

	  ENDIF		;Rsx
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;	Patch Area Header (doubles as RSX header)
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

JTABLE:	JP	0		; [BDOS for RSX]
MODWB:	JP	INSTALL		; Install routine [WBOOT for RSX]
MODRMV:	JP	REMOVE		; Remove routine
WBBIOS:	DEFW	0		; Next module install [WBOOT addr for RSX]
RSXPTA:	DEFW	0		; Previous module remove [protect addr for RSX]
	DEFW	MODID		; Text header for module
WBNEXT:	DEFB	'PCH'		; Patch signature for NZCOM [JP NXTWB for RSX]
NXTLOD:	DEFW	TOP		; Last address in the module + 1
VERIFY:	DEFW	JTABLE		; Pointer to head of table for verify

; The following vectors are copied to the ZSDOS Date Vector Area

JRWCLK:	DEFW	RWCLK		; Clock read/write routine
	DEFW	STLA		; Stamp file last access
	DEFW	STCR		; Stamp file create
	DEFW	STUP		; Stamp file modify
	DEFW	RSTAMP		; Get stamp
	DEFW	WSTAMP		; Put stamp

MODID:	DEFB	'ZDS V',VER/10+'0','.',[VER MOD	10]+'0',0

	PAGE
	  IF	  RSX
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
; Routine to check if this RSX is lowest RSX in memory,
; Restores Warm Boot Address at 0001H
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

CKWARM:	LD	DE,MODWB
	LD	HL,(WBBIOS)
	LD	(0001H),HL	; Restore warm boot
	INC	HL
	LD	A,(HL)
	INC	HL
	SUB	E
	RET	NZ
	LD	A,(HL)
	SUB	D		; Set Zero if this is lowest module
	RET

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;     R S X    W a r m    B o o t    R o u t i n e
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

MODWARM: CALL	CKWARM		; Restore WB, check if we are lowest RSX
	JR	NZ,MODWM1	; If not lowest
	LD	HL,(RSXPTA)
	LD	(0006H),HL	; Protect address
MODWM1:	LD	A,(WBNEXT+2)	; Get pointer to next WB routine
	LD	HL,WBBIOS+1	; Get base page of BIOS
	CP	(HL)		; See if below BIOS
	JR	C,WBNEXT	; If below, must be another RSX
	LD	A,(0004H)	; Get logged DU:
	LD	C,A
	LD	D,(HL)
	DEC	HL
	LD	E,(HL)		; Get BIOS WBOOT address
	LD	HL,-[0E00H+800H] ; Offset from WBOOT to CCP+3
	ADD	HL,DE		; Point to CCP+3
CALLHL:	JP	(HL)		; And exit to CCP

	PAGE
	  ELSE
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;	  I n s t a l l   Z D S    R o u t i n e
;..........................................................
; Enter: BC = BIOS Warm Boot address (from location 0001,2)
;	 DE = Environment address or 0000 if none
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

INSTALL: PUSH	BC
	PUSH	DE		; SAVE Params for others
	LD	A,D
	OR	E		; Was an ENV passed?
	JR	Z,INSTA0	; Jump if no pointer, assume normal system
	LD	HL,8
	ADD	HL,DE		; Point to ENV Type
	BIT	7,(HL)		; Is this an extended ENV?
	JR	Z,INSTA0	; ..jump if no Extended ENV
	LD	HL,42H		; Point to DOS address
	ADD	HL,DE		; Point to ENV Type
	LD	A,(HL)
	INC	HL
	LD	H,(HL)		; Get DOS address from ENV
	LD	L,A
	JR	INSTA1		; DOS offset MSB in B

INSTA0:	LD	C,0		; Insure BIOS is page boundary
	LD	HL,-0E00H	; Subtract DOS size
	ADD	HL,BC		; DOS starting page now in HL
INSTA1:	LD	(OFFSET),HL	; Let ZDS know where things are
	PUSH	HL		; Save DOS address
	LD	DE,6		; Offset to DOS entry jump
	ADD	HL,DE
	LD	C,48		; Get Version function
	CALL	CALLHL		; ..and do it
	CP	DOSVER		; Is it the right version?
	JR	NZ,CHKDO2	; ..jump if not
	LD	A,H
	CP	DOSID		; Is it the correct DOS?
CHKDO2:	POP	DE
	JR	NZ,INSNX1	; Skip install if wrong DOS
	LD	HL,LOADED
	LD	A,(HL)
	AND	A
	JR	NZ,INSNX1	; Quit if we are loaded already
	DEC	(HL)		; Else flag loaded

; We need to do this here in case NZCOM is used.

	LD	HL,16H		; Offset to table
	ADD	HL,DE
	EX	DE,HL		; ..and put in DE
	LD	HL,JRWCLK
	LD	BC,12
	LDIR			; ..and move vectors to DOS table
	EX	DE,HL		; Move current DOS table addr to HL
	INC	HL
	INC	HL		; Point to DOS Remove Storage
	LD	DE,MODRMV	; Set Module Remove Vector
	LD	(HL),E		; ..and save in DOS
	INC	HL
	LD	(HL),D
	CALL	INSTA3		; Install patches into DOS
INSNX1:	LD	HL,(WBBIOS)
INSNXT:	POP	DE
	POP	BC		; Restore Registers
	LD	A,H
	OR	L		; Test for another module present
	RET	Z		; Exit if no others
CALLHL:	JP	(HL)		; ..else install next
	  ENDIF		;rsx

; Patch ZDS into/out of DOS

INSTA3:	LD	BC,ZDPCH1
	LD	DE,ZDPC1T
	CALL	PATCH
	LD	BC,ZDPCH2
	LD	DE,ZDPC2T
	CALL	PATCH
	LD	BC,ZDPCH3
	LD	DE,ZDPC3T
PATCH:	LD	HL,(OFFSET)
	ADD	HL,BC		; Point to DOS patch point
	LD	B,3		; Every patch is 3 bytes
PATCH1:	LD	C,(HL)		; Fetch first byte from DOS
	LD	A,(DE)
	LD	(HL),A		; Install patch in DOS
	LD	A,C
	LD	(DE),A		; And into ZDS
	INC	HL
	INC	DE		; Bump pointers
	DJNZ	PATCH1		; ..and loop
	RET

	PAGE
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;	  R e m o v e    Z D S    R o u t i n e
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

REMOVE:	PUSH	BC
	PUSH	DE
LOADED	EQU	$+1
	LD	A,0		; Store LOADED flag inline
	AND	A		; Test if loaded
	JR	Z,REMOV2	; ..jump if we aren't loaded
	XOR	A
	LD	(LOADED),A	; Else flag not loaded anymore
	LD	HL,(OFFSET)
	LD	DE,UNLOAD+1	; Point to MSB of REMOVE addr in ZSDOS table
	ADD	HL,DE
	LD	(HL),A
	DEC	HL
	LD	(HL),A		; Clear the Remove address in DOS
	DEC	HL		; Point to MSB of DUMMY
	LD	D,(HL)
	DEC	HL
	LD	E,(HL)		; Dummy value in DE
	LD	B,6
REMOV1: DEC	HL
	LD	(HL),D
	DEC	HL
	LD	(HL),E		; Replace entry with dummy value
	DJNZ	REMOV1		; Until done
	CALL	INSTA3		; And remove patches
REMOV2:	LD	HL,(RSXPTA)	; Get address of next module to remove

	  IF  RSX
	CALL	CKWARM		; See if we're lowest RSX - includes ZEX
	JR	NZ,REMOV3	; Don't allow removal if there are others
	DI			; Hold things off while we work
	LD	HL,(JTABLE+1)	; Get prior BDOS vector
	LD	(0006),HL	; Restore it
	LD	HL,(WBBIOS)	; Get BIOS entry address
	INC	HL
	LD	DE,(WBNEXT+1)	; Get warm boot of next RSX up
	LD	(HL),E
	INC	HL
	LD	(HL),D		; Patch it into BIOS
	EI			; Ok, back on now
	SCF			; Set carry to indicate success
	DEFB	0EH		; ..fall thru the XOR  A with a LD  E,AF

REMOV3:	XOR	A		; Clear carry to show error
REMOV4:	POP	DE
	POP	BC
	RET			; ..and exit
	  ELSE
	JR	INSNXT		; Quit
	  ENDIF		;rsx
	PAGE
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;		S t a m p     C r e a t e
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

STCR:	LD	B,00		; Offset into stamp in B
	JR	STT

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;		S t a m p     U p d a t e
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

STUP:	LD	B,10		; Offset into stamp in B
	JR	STT

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;		S t a m p     A c c e s s
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

STLA:	LD	B,05		; Offset into stamp in B
STT:	LD	(INDEX),A	; Save index into DIR entry
	LD	(DMA),HL	; Save user DMA pointer
	LD	C,2		; Show as stamp
	LD	H,0
	ADD	A,3		; Point to no date attribute
	LD	L,A
	ADD	HL,DE		; Point to DIR entry
	BIT	7,(HL)
	LD	A,0FFH		; Prepare error status in case
	RET	NZ		; Don't update if no date attrib
	JR	STPSV0

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;     G E T / P U T    S t a m p    R o u t i n e s
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

RSTAMP:	LD	BC,0		; Flag as read
	JR	STPSVC

WSTAMP:	LD	BC,1		; Flag as write
			; And fall thru...

; Stamp Service Routine (Combined Get Stamp and Put Stamp)
; Calling Parameters:
;	A = Index to DIR Entry [00H,20H,40H,60H]
;	B = Offset in Stamp [0,5,10]
;	C = Function [0 = Read, 1=Write, 2=Update]
;	DE= Pointer to DIR Buffer
;	HL= User DMA Address

STPSVC:	LD	(INDEX),A	; Save index into DIR entry
	LD	(DMA),HL	; Save user DMA pointer
STPSV0:	LD	(RWCODE),BC	; Save read/write code and Sector offset
	LD	(SECBUF),DE	; Check if !!!TIME&.DAT Exists on the drive
	CALL	GETTDV		; Return current time/date vector in BC
	PUSH	BC		; Save it
	LD	DE,GETCDM
	CALL	VECTOR		; Return vector for current drive in hl
;==	POP	HL
	pop	bc		; [1.1]
	LD	A,H
	AND	B
	LD	H,A
	LD	A,L
	AND	C
	OR	H		; Test if current drive has !!!TIME&.DAT
	JR	Z,ERROR		; ..jump if it doesn't

SECBUF	EQU	$+1
	LD	BC,0000		; Store Sector Buffer adr inline
	CALL	DODMA		; Set DMA to address sector buffer

; It exists, so find first alloc for it (First after DIR)

	LD	DE,RECDIR
	CALL	INDFET		; Get current dir record
			; Calculate Buffer Offset
	XOR	A
	SRL	B
	RR	C		; Divide record by two, lsb to carry
	RRA			; Place in msb of a
	LD	HL,INDEX
	ADD	A,(HL)		; Get offset to stamp in buffer
	RRA			; Divide by 2 (8 stamps/rec)
	LD	(HL),A		; Save index for later
			; Now calculate record number needed
	PUSH	BC		; Save relative record number
	LD	DE,DPBOF+9	; Get dir alloc bitmap from DPB
	CALL	INDFET
	PUSH	BC		; Save bitmap
	LD	DE,DPBOF+3	; Point to block mask in DPB
	CALL	INDFET		; Get into C reg
	INC	C		; Now is number of records per block
	XOR	A		; Cheap zero
	LD	E,C		; Records/block to E
	LD	D,A		; Sign extend
	LD	B,16		; Check all bits
	POP	HL		; Restore bitmap, rel rec on tos
STPSV1:	ADD	HL,HL		; Shift MSB into Carry
	JR	NC,STPSV2	; If bit was a zero
	EX	(SP),HL		; Else get relative record
	ADD	HL,DE		; Add records for this dir block
	EX	(SP),HL		; Back to tos
STPSV2:	DJNZ	STPSV1		; Loop until done
	POP	HL		; now has actual record number
			; Call DSSTS (Set BIOS Track and Sector)
	CALL	RDSEC		; Set Track/Sector and Read
	CALL	CKSUM		; Checksum first 127 bytes of local buffer
	CP	(HL)		; Test against DS's checksum
	JR	NZ,ERROR	; Abort if error
			; Set Pointers for Block Move
	XOR	A		; Clear acc
	LD	D,A		; Word value for D
	LD	B,A		; And B

INDEX	EQU	$+1
	LD	E,00		; INDEX value stored here inline
	LD	HL,(SECBUF)
	ADD	HL,DE		; Now pointing to correct stamp
	LD	C,15		; Size of stamp
	LD	DE,(DMA)	; Get user stamp pointer
			; Test for Why we're here
	LD	A,(RWCODE)	; Get Read/Write Code
	AND	A		; Is it get stamp?
	JR	NZ,PUTSTP	; If we're supposed to write
			; Copy Stamp from Local Buff to User DMA
	LDIR			; Move to user DMA
	JR	STPSVX		; ..and exit

; Copy Stamp from User DMA to Local Buff

PUTSTP:	SUB	2		; Is this a simple put?
	JR	Z,UPSTMP	; No, read the clock
	EX	DE,HL		; Swap pointers
PUTS1:	LDIR			; Move new stamp into place
	CALL	CKSUM		; Checksum first 127 bytes of local buffer
	LD	(HL),A		; ..and save checksum in last byte of record

	LD	E,WRSECT	; Do BIOS Write Sector
	INC	C		; ..Flag as non-deferred (0 from ldir -> 01)
	CALL	DOBIOSE		; Write sector to disk

STPSVX:	LD	A,1		; Indicate all is well (OK return)
	DEFB	01H	; ..fall thru the LD  A,FF with a LD  BC,FF3E

; Set Return to Error

ERROR:	LD	A,0FFH		; Set Error condition and restore User DMA
EXIT:	PUSH	AF		; Save return code

DMA	EQU	$+1
	LD	BC,0000		; Get User's DMA addr (stored here inline)
	CALL	DODMA		; ..and restore it
	POP	AF		; Restore return code
	RET			; ..and return to ZSDOS

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;    Read the Clock and place in proper Stamp Field
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

UPSTMP:	LD	D,A		; A reg contains 0
	LD	A,(STOFF)	; Get offset in stamp
	LD	E,A		; Make word length in DE
	ADD	HL,DE		; Point to where to put stamp
	PUSH	HL		; Save target address
	OR	A		; Is it CREATE time?
	JR	NZ,UPSTM0	; ..jump if Not
	LD	B,15		; Else zero entire stamp record
UPSL:	LD	(HL),0
	INC	HL
	DJNZ	UPSL
UPSTM0:	LD	C,D		; Flag as clock read
	LD	DE,TDBUF	; Point to TD buffer
	PUSH	DE
	CALL	RWCLK		; Fetch the time (may be BIOS)
	POP	HL		; Get buffer start
	POP	DE		; Get stamp address
	DEC	A		; Was it a good clock read?
	JR	NZ,ERROR	; ..Take error exit if not
	LD	BC,5		; Else move 5 bytes
	JR	PUTS1		; ..and finish up

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;    Checksum the first 127 bytes of the Local Buffer
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

CKSUM:	LD	B,127
	LD	HL,(SECBUF)	; Point to buffer start
	XOR	A		; Init checksum to 0
CKSUM1:	ADD	A,(HL)		; Add to checksum
	INC	HL
	DJNZ	CKSUM1		; Loop until done
	RET

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
; Read Sector Number in HL Using BIOS to Current DMA
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

RDSEC:	EX	DE,HL		; Move desired logical record to DE
	LD	BC,STDIR1	; DOS logical record -> track/sector
	LD	HL,(OFFSET)
	ADD	HL,BC		; ZSDOS Stdir1 address now in HL
	CALL	CALLHL		; Set Track/Sector
	LD	E,RDSECT	; Do BIOS Read of Sector
			;..fall thru to READ
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;  C a l l   B I O S   w i t h   E r r o r   T e s t i n g
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

DOBIOSE: CALL	DOBIOS		; Do the function
	AND	A		; Was there an error?
	RET	Z		; ..return if no error
	POP	AF		; Else clear return address from stack
	JR	ERROR		; Flag error and return

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
; Indexed Fetch of BC from ZSDOS RAM
;  Called w/ DE=Offset in ZSDOS, Returns BC=Value at Index
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

GETTDV:	LD	DE,TDFVCT	; Time and date vector - commonly used
INDFET:	LD	HL,(OFFSET)
	ADD	HL,DE		; Point to LSB
	LD	C,(HL)
	INC	HL
	LD	B,(HL)		; Get value from RAM
	RET

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;     R e p l a c e m e n t   F u n c t i o n   12
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

NEWF12:	LD	A,E		; See if arg passed
	CP	'D'
ZDPC1T:	CALL	NEWF12		; Gets swapped with DOS code
;--	LD	HL,22H		;   (DOS code swapped)
	RET	NZ		; If DS inquery not used
	LD	H,E		; Set return value
	LD	DE,CLK		; Address of clock routine
	RET

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;     C h e c k    f o r    ! ! ! T I M & . D A T
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

SETCHK:	LD	A,0FFH
	LD	(CKDSF),A	; Set flag to check DS
ZDPC2T:	CALL	SETCHK		; This gets swapped with DOS
;--	CALL	SETFCT		;   (DOS code swapped)
	RET

CKDSF	EQU	$+1
CKTDF:	LD	A,00		;   <CKDSF flag stored inline>
	OR	A		; Have we passed this way before?
	JR	Z,ZDPC3T	; ..exit if we have
	INC	A
	LD	(CKDSF),A	; Else clear flag
	LD	BC,0C00H	; Init loop count and checksum
	PUSH	HL		; Get DIR pointer back
CKTDF1:	LD	A,(HL)		; Get char from directory
	AND	7FH		; Mask attributes
	ADD	A,C
	LD	C,A		; Update Checksum
	INC	HL		; bump pointers
	DJNZ	CKTDF1		; And loop
	CP	TDVAL		; Check against !!!TIME&.DAT checksum
	JR	NZ,CKTDFX	; ..exit if no match
	LD	DE,TDFVCT
	CALL	GETTDV		; Get time/date login vector in BC
	PUSH	HL		; Save pointer to MSB of vector
	LD	DE,SDRVB	; Prepare to add this drive to vector
	LD	H,B
	LD	L,C		; Xfer time/date login mask to HL
	CALL	VECTOR		; HL=new time/date vector on return
	POP	DE		; Restore pointer to Vector storage
	EX	DE,HL		; New T/D vector in DE
	LD	(HL),D
	DEC	HL
	LD	(HL),E		; Update the vector in DOS
CKTDFX:	POP	HL
ZDPC3T:	CALL	CKTDF		; Gets swapped with DOS code
;--	CALL	CKSUB		;   (DOS code swapped)
	RET

;.....
; Set DMA address.

DODMA:	LD	E,SETDMA	; Set offset for BIOS call
			;..and fall thru
;.....
; Do BIOS Call.  Enter with BIOS offset in E, parameter in BC

DOBIOS:	LD	D,BIOS/256	; Set BIOS Page address in MSB
VECTOR:	PUSH	HL		; Save caller's HL

OFFSET	EQU	$+1
	LD	HL,0000		; Get ZSDOS base address (stored inline)
	ADD	HL,DE		; Now pointing to BIOS base
	EX	(SP),HL		; Restore HL, vector to TOS
	RET			; ..and execute BIOS function

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;		R A M    A r e a
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

RWCODE:	DEFB	0		; 0 for read stamp, 1 for write stamp
STOFF:	DEFB	0		; Offset in stamp
TDBUF:	DEFS	6		; Temp storage for date

	PAGE
;**********************************************************
; Clock Driver routines for ZSDOS
;
; The routine placed here implements the clock driver for
; ZSDOS/DateStamper.  The target of JRWCLK points to this
; location.  The actual routine exists here, appended by
; SETUPZST, and may be a complete driver or may point to
; routines implemented in BIOS.
;
; If a DateStamper "type" clock driver is being added,
; SETUPZST will set the code to return error status on a
; clock set function while the code is altered to enable
; clock setting if a ZSDOS "type" clock is selected.
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
; The interface specification at this point is:
;
; Entry Parameters:
;	C = Read/Write Code (1=Write, 0=Read)
;	DE= Address to Put Time
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

	DEFS	8-[[$-JTABLE] MOD 8]	; End on boundary

RWCLK:	EX	DE,HL		; Put address for Time in HL
	LD	A,C		; Is it a set or read?
	OR	A
	JR	Z,CLK		; ..jump to Read if 0

; The following code sets error status on a Set Time for DateStamper
; clocks.  It is overwritten with a relative jump to CLK+3 if ZSDOS
; style clock is appended.

	XOR	A		; Set error status
	DEC	A		; ..to 0FFH
	RET

;.....
; The remaining memory in this module is overwritten by
;  the clock driver.

CLK:

TOP	EQU	$

	END
