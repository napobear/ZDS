	TITLE	"CP/M+ (P2DOS) Time for ZSDOS"
;===============================================================;
;  NZT/P2D - ZSDOS Driver for P2DOS and NZTime Stamps		;
;---------------------------------------------------------------;
; Copyright (C) 1988  by Harold F. Bower and Cameron W. Cotrill	;
;---------------------------------------------------------------;
; Portions of this code were derived from code written by	;
;	 H.A.J. Ten Brugge					;
;								;
; FUNCTION:   To convert from DateStamper(tm) "type"  date/time	;
; string  to  the  5-byte date/time  string required for P2DOS.	;
; This code provides the time functions for ZSDOS to operate in	;
; a P2DOS (CP/M Plus compatible) type Date/Time stamping mode.	;
;								;
; Universal Time String	    :	YY MM DD HH MM SS  (all BCD)	;
;								;
; CP/M+ (P2DOS) Time String :	nnnn HH MM SS			;
;    nnnn = binary number of days since start (1 = 1 Jan 1978)	;
;				HH MM SS = time in BCD		;
;								;
; NZTime : Bit-mapped version of UTS.  See descr at NZRSTP	;
;								;
; Version:							;
;	1.0 - Initial Release			16 Sep 88	;
;	2.02- Added NZTime to P2Dos selection	10 Jan 99	;
;===============================================================;

VER	EQU	20		; Initial Release

FALSE	EQU	0
TRUE	EQU	NOT FALSE

	MACLIB	RSXFLAG		; Get the definition of RSX equate flag
				; TRUE=Assemble as RSX, FALSE=Hi-memory module
				;== ALSO VERSION DEPENDENT ADDRESSES

DOSID	EQU	'S'		; ID tag for ZSDOS
DOSVER	EQU	11H		; ZSDOS version number for this module
	PAGE
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;  S T A R T    O F    R E L O C A T A B L E    C O D E
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

BEGIN:				; Label for ending space calculations

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
; RSX Install Routine - Used once, then no longer needed.
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

	  IF	  RSX
INSTALL: PUSH	BC
	PUSH	DE		; Save params for others
	LD	A,D
	OR	E		; Is there an Environment?
	JR	Z,INST00	; ..jump if none 'cause can't be NZCOM
	LD	HL,8
	ADD	HL,DE		; Point to ENV type
	BIT	7,(HL)		; Is it an extended Environment?
	JR	Z,INST00	; ..jump if Not
	LD	HL,42H		; Point to DOS Hi-addr in Extended Env
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A		; ..and get to HL
	JR	INST01		; DOS offset now in HL

INST00:	LD	C,0		; Insure BIOS is even page
	LD	HL,-0E00H	; Subtract DOS size in pages
	ADD	HL,BC		; DOS starting addr now in HL
INST01:	LD	(OFFSET),HL	; Let LOADER know where things are
	PUSH	HL
	LD	DE,6		; Offset to DOS entry jump
	ADD	HL,DE
	LD	C,48		; Get Version command
	CALL	CALLHL		; ..and do it
	CP	DOSVER		; Is it the correct version?
	JR	NZ,INST02	; ..jump if not
	LD	A,H		; Get type flag byte
	CP	DOSID		; Is it our type?
INST02:	POP	DE		; Restore DOS base addr
	JR	NZ,INSNXT	; Skip install if wrong DOS
	LD	HL,LOADED	; Are we loaded already?
	LD	A,(HL)
	AND	A
	JR	NZ,INSNXT	; If we are loaded already
	DEC	(HL)		; Else flag loaded
	CALL	INSRSX		; Set up header and tie in
	LD	HL,16H		; Offset DOS to table
	ADD	HL,DE
	EX	DE,HL		; ..and put in DE
	LD	HL,JRWCLK	; Where to copy from
	LD	BC,12		; Move 12 bytes
	LDIR
	EX	DE,HL		; Put DOS table addr in HL
	INC	HL
	INC	HL		; Point to DOS Remove Storage
	LD	DE,MODRMV	; Set module Remove Vector
	LD	(HL),E		; ..and save in DOS table
	INC	HL
	LD	(HL),D
INSNXT:	POP	DE
	POP	BC
	RET

INSRSX:	DI			; Don't interrupt during this
	LD	HL,MODWARM	; Get address of module warm boot
	LD	(MODWB+1),HL	; Place module boot address
	LD	HL,(0006H)	; Get top of memory
	LD	(JTABLE+1),HL
	LD	HL,JTABLE
	LD	(RSXPTA),HL	; Set protect address
	LD	(0006H),HL
	LD	HL,(0001H)
	LD	(WBBIOS),HL	; BIOS Warm Boot target
	INC	HL
	LD	C,(HL)
	INC	HL
	LD	B,(HL)		; Get where WBOOT is pointing
	PUSH	HL
	LD	HL,WBNEXT
	LD	(HL),0C3H	; JP Opcode
	INC	HL
	LD	(HL),C
	INC	HL
	LD	(HL),B		; Place jump to next WBOOT routine
	POP	HL
	LD	BC,MODWB
	LD	(HL),B
	DEC	HL
	LD	(HL),C		; Install ourself into BIOS warm boot
	EI			; Ok, we're tied in - make my day
	RET

	  ENDIF		;Rsx

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
; Patch Area Header (doubles as RSX header)
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

JTABLE:	JP	0		; [BDOS for RSX]
MODWB:	JP	INSTALL		; Install routine [WBOOT for RSX]
MODRMV:	JP	REMOVE		; Remove routine
WBBIOS:	DEFW	0		; Next module install [WBOOT addr for RSX]
	  IF  RSX
RSXPTA:	DEFW	MODWARM		; Previous module remove [protect addr for RSX]
	  ELSE
RSXPTA:	DEFW	0		; Previous module remove [protect addr for RSX]
	  ENDIF
	DEFW	MODID		; Text header for module
WBNEXT:	DEFB	'PCH'		; Patch signature for NZCOM [JP NXTWB for RSX]
NXTLOD:	DEFW	TOP		; Last address in the module + 1
VERIFY:	DEFW	JTABLE		; Pointer to head of table for verify

; The following entries replicate ZSDOS table at offset 16H

JRWCLK:	DEFW	RWCLK		; Read/Set Real-Time Clock
STLA:	DEFW	NOTIM0		; Stamp Last Access Time (Points to error ret)
STCR:	DEFW	STAMPC		; Stamp Create Time
STUP:	DEFW	STAMPU		; Stamp Update Time
SGSTMP:	DEFW	GSTAMP		; Get File Stamp
SPSTMP:	DEFW	PSTAMP		; Put File Stamp


MODID:	DEFB	'P2DOS Type, v'
	DEFB	VER/10+'0','.',[VER MOD 10]+'0',0

	PAGE
	  IF	  RSX
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
; Routine to check if this RSX is lowest RSX in memory,
; Restores Warm Boot Address at 0001H
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

CKWARM:	LD	DE,MODWB
	LD	HL,(WBBIOS)
	LD	(0001H),HL	; Restore warm boot
	INC	HL
	LD	A,(HL)
	INC	HL
	SUB	E
	RET	NZ
	LD	A,(HL)
	SUB	D		; Set Zero flag if we are bottom module
	RET

;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;     R S X    W a r m    B o o t    R o u t i n e
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

MODWARM: CALL	CKWARM		; Restore WB, check if we are lowest RSX
	JR	NZ,MODWM1	; If not lowest
	LD	HL,(RSXPTA)
	LD	(0006H),HL	; Protect address
MODWM1:	LD	A,(WBNEXT+2)	; Get pointer to next WB routine
	LD	HL,WBBIOS+1	; Get base page of BIOS
	CP	(HL)		; See if below BIOS
	JR	C,WBNEXT	; If below, must be another RSX
	LD	A,(0004H)	; Get logged DU:
	LD	C,A
	LD	D,(HL)
	DEC	HL
	LD	E,(HL)		; Get BIOS WBOOT address
	LD	HL,-[0E00H+800H] ; Offset from WBOOT to CCP+3
	ADD	HL,DE		; Point to CCP+3
CALLHL:	JP	(HL)		; And exit to CCP

	PAGE
	  ELSE
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;	  I n s t a l l   Z D S    R o u t i n e
;..........................................................
; Enter: BC = BIOS Warm Boot address (from location 0001,2)
;	 DE = Environment address or 0000 if none
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

INSTALL: PUSH	BC
	PUSH	DE		; SAVE Params for others
	LD	A,D
	OR	E		; Was an ENV passed?
	JR	Z,INSTA0	; Jump if no pointer, assume normal system
	LD	HL,08H		; ..offset..
	ADD	HL,DE		; Point to ENV Type
	BIT	7,(HL)		; Is this an extended ENV?
	JR	Z,INSTA0	; ..jump if no Extended ENV
	LD	HL,42H		; ..offset to DOS
	ADD	HL,DE
	LD	A,(HL)
	INC	HL
	LD	H,(HL)		; Get DOS address from ENV
	LD	L,A
	JR	INSTA1		; DOS offset now in HL

INSTA0:	LD	C,0		; Insure BIOS is page boundary
	LD	HL,-0E00H	; Subtract DOS size
	ADD	HL,BC
INSTA1:	LD	(OFFSET),HL	; Let ZDS know where things are
	PUSH	HL		; Save DOS address
	LD	DE,6		; Offset to DOS entry jump
	ADD	HL,DE
	LD	C,48		; Get Version function
	CALL	CALLHL		; ..and do it
	CP	DOSVER		; Is it the right version?
	JR	NZ,CHKDO2	; ..jump if not
	LD	A,H
	CP	DOSID		; Is it the correct DOS?
CHKDO2:	POP	DE
	JR	NZ,INSNX1	; Skip install if wrong DOS
	LD	HL,LOADED
	LD	A,(HL)
	AND	A
	JR	NZ,INSNX1	; Quit if we are loaded already
	DEC	(HL)		; Else flag loaded

; We need to do this in case NZCOM is used.

	LD	HL,TBLOFF	; Offset to table
	ADD	HL,DE
	EX	DE,HL		; ..and put in DE
	LD	HL,JRWCLK
	LD	BC,12
	LDIR
	EX	DE,HL		; ZSDOS Dummy address to HL
	INC	HL
	INC	HL		; Advance to UNLOAD vector address
	LD	DE,MODRMV	; Point to Remove routine
	LD	(HL),E		; ..and store address in ZSDOS
	INC	HL
	LD	(HL),D		; Save Remove in DOS
INSNX1:	LD	HL,(WBBIOS)
INSNXT:	POP	DE
	POP	BC		; Restore Registers
	LD	A,H
	OR	L		; Test for another module present
	RET	Z		; Exit if no others
CALLHL:	JP	(HL)		; ..else install next
	  ENDIF		;Rsx (else)

	PAGE
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
; Remove module by disabling all Timestamp vector table addresses
;::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::::

REMOVE:	PUSH	BC
	PUSH	DE
LOADED	EQU	$+1
	LD	A,00		; Storage for flag in code
	OR	A		; Is it loaded?
	JR	Z,REMOV0	; ..jump if not
	XOR	A		; Else flag as gone
	LD	(LOADED),A
OFFSET	EQU	$+1
	LD	HL,0000		; Get DOS address
	LD	DE,UNLOAD+1	; ..and offset to UNLOAD vector
	ADD	HL,DE
	LD	(HL),A		; Clear the ZSDOS Unload vector
	DEC	HL
	LD	(HL),A
	DEC	HL
	LD	D,(HL)		; Get disable vector..
	DEC	HL
	LD	E,(HL)
	LD	B,6
RMLOOP:	DEC	HL
	LD	(HL),D		; Move disable vector to memory
	DEC	HL
	LD	(HL),E
	DJNZ	RMLOOP		; Loop til all vectors disabled
REMOV0:	LD	HL,(RSXPTA)	; Get address of next module to remove
	  IF  RSX
	CALL	CKWARM		; See if we're lowest RSX - includes ZEX
	JR	NZ,REMOV1	; Don't allow removal if there are others
	DI			; Hold things off while we work
	LD	HL,(JTABLE+1)	; Get prior BDOS vector
	LD	(0006),HL	; Restore it
	LD	HL,(WBBIOS)	; Get BIOS entry address
	INC	HL
	LD	DE,(WBNEXT+1)	; Get warm boot of next RSX up
	LD	(HL),E
	INC	HL
	LD	(HL),D		; Patch it into BIOS
	EI			; Ok, back on now
	SCF			; Set carry to indicate success
	DEFB	0EH		; Trash "C" and fall thru to REMOV2
REMOV1:	XOR	A		; Clear carry to show error
REMOV2:	POP	DE
	POP	BC
	RET			; ..and exit
	  ELSE
	JR	INSNXT		; Quit
	  ENDIF		;Rsx

	PAGE
;===============================================================+
; Stamp CREATE/UPDATE Time Field in T&D. (Extracted from PZDOS)	|
; ------------------------					|
;   Read the Real Time Clock via SGSTIM vector in Universal	|
;   format to buffer, convert to CP/M+ (P2DOS) format, and	|
;   move to appropriate field in DIR Buffer.			|
;---------------------------------------------------------------|
; Enter: A = Directory Offset (0, 20H, 40H) of subject file	|
;	BC = Address of WRFCB routine in ZSDOS			|
;	DE = Pointer to DIR Buffer.  (Offset in A)		|
;								|
; Exit : A = 1 if OK, Else A = 0FFH if error.  Flags undefined	|
;								|
; Effects: Current DMA Buffer altered				|
;===============================================================+
;========================================================================
;		P 2 D O S   (CP/M+)   S T A M P   C O D E		;
;========================================================================
; Portions of this code were extracted from  DATE.ASM  provided with the
; P2DOS diskette.				By H.A.J. Ten Brugge
;
; FUNCTION:   To convert between DateStamper(tm) "type"  date/time string
; and 5-byte date/time strings required for CP/M+.
;
; Universal Time String	    :	YY MM DD HH MM SS  (all BCD)
;
; CP/M+ (P2DOS) Time String :	nnnn HH MM SS
;    nnnn = binary number of days since start (1 = 1 Jan 1978)
;				HH MM SS = time in BCD
;========================================================================
; Stamp CREATE/UPDATE Time Field in T&D. (Extracted from PZDOS)
;
; Read the Real Time Clock via SGSTIM vector in Universal format to buffer,
; convert to CP/M+ (P2DOS) format, and move to appropriate field in DIR Bufr.
;
; Enter: None
; Exit : A = 1 if OK, Else A = 0FFH if error.  Flags undefined
; Effects: Current DMA Buffer altered
;========================================================================

STAMPC:	LD	L,0		; Set to Create field in Stamp
	JR	STTIM		; ..jump to Common Code

STAMPA:	LD	L,2		; Index to Last Access field in NZTIM (+1)
	JR	STTIM		; ..jump to Common Code

STAMPU:	LD	L,4		; Set to Update field in Stamp
STTIM:	CALL	SETREC		; Use DE offset to rec in A.  Save L in E
NZSTMP:	LD	A,$-$		;2.02 .(argument set in SETREC)
	OR	A		;2.02 Are we dealing with P2Dos Stamps?
	LD	A,E		;2.02 .(get offset for checks)
	JR	Z,NZST0		;2.02 ..jump if P2Dos Stamps
				;2.02 Continue if NZTIM Stamps
	CP	2		;2.02 Is this Create?
	JR	C,NZST0		;2.02 ..jump if so (A=0)
	LD	A,3		;2.02 (prepare for Access)
	JR	Z,NZST0		;2.02 ..jump if Last Access (A=3)
	LD	A,6		;2.02 Else it is Update, set Offset (A=6)

NZST0:	LD	C,A		;2.02 Save
	CP	2		;2.02 Is it P2Dos Last Access (Error)
	JR	Z,NOTIM0	;2.02 ..jump to Return FF if So

	ADD	HL,BC		; Destination of 4-byte T&D now in HL
	LD	DE,DSTIME	; Set address to read time
	PUSH	HL		; .save destination addr
	PUSH	DE		; ..and source addr
	PUSH	BC		;2.02 ..and Counter
	LD	C,B		; .(set C=0 for Clock Read)
	CALL	RWCLK		; Read the Clock
	POP	BC		;2.02 .restore Counter
	POP	DE		; .restore source addr
	POP	HL		; ..and destination addr
	DEC	A		; Was the clock read Ok?
	JR	NZ,NOTIM0	; ..jump if Not to Err exit
	LD	A,(NZSTMP+1)	;2.02
	OR	A		;2.02 Are we stamping NZTIM?
	JP	NZ,NZTUP	;2.02 ..jump if So
	CALL	U2PTIM		; Cv Univ. time (DE) to CP/M+ time field (HL)
	JR	WRFCB0		; Write FCB, Set OK return flags and exit

;===============================================================+
; Put File Stamps in Universal Format				|
; ---------------						|
;   Convert Create and Update Time fields from Universal format	|
;   to CP/M+ (P2DOS) form and insert in DIRectory buffer.  Call	|
;   WRFCB routine to write Directory on exit.			|
;---------------------------------------------------------------|
; Enter: A = Directory Offset (0, 20H, 40H) of subject file	|
;	BC = Address of WRFCB Routine in ZSDOS			|
;	DE = Pointer to DIR Buffer.  (Offset in A)		|
;	HL = DMA Address containing 15 byte Stamp frame		|
;								|
; Exit : A = 1 if OK, Else A = 0FFH if error.  Flags undefined	|
;								|
; Effects : Addressed DIR buffer updated on disk if successful	|
;===============================================================+

PSTAMP:	CALL	SETREC		; Calculate the Stamp area addr for file
	LD	A,(NZSTMP+1)	;2.02
	OR	A		;2.02 Using NZTime Stamps?
	JP	NZ,NZWSTP	;2.02 ..jump if So
	CALL	U2PTIM		; Convert Create field to destination
	JR	NZ,NOTIM0	; ..jump to Error exit if Invalid Date
	INC	DE		; Bypass Last Access field of input
	INC	DE
	INC	DE
	INC	DE
	INC	DE
	CALL	U2PTIM		; Convert Update field to destination
WRFCB0:	JR	NZ,NOTIM0	; ..jump to Error Exit if Invalid Date
WRFCB:	CALL	$-$		; Address set on entry
OKRET:	LD	A,1		; Else set OK flags and return
	RET

;.....
NOTIM:	POP	AF		; Clear the Stack
NO$TD:	POP	AF		; Remove Return Addr from Stack
NOTIM0:	OR	0FFH		; ..and set error flags
	RET			; Back to caller

;===============================================================+
; Get File Stamps in Universal Format				|
; ---------------						|
;   Read the Create and Update Stamps and convert to Universal	|
;   format in the proper fields at the specified address.  Null	|
;   the Last Access Time field.					|
;---------------------------------------------------------------|
; Enter: A = Directory Offset (0, 20H, 40H) of subject file	|
;	DE = Pointer to DIR Buffer.  (Offset in A)		|
;	HL = DMA Address to receive 15 byte Stamp frame		|
;								|
; Exit : A = 1 if OK, Else A = 0FFH if error.  Flags undefined	|
;								|
; Effects : DMA Buffer contains the 15-byte frame if successful	|
;===============================================================+

GSTAMP:	CALL	SETREC		; Calculate Source T&D address to HL
				; .(Errors bypass return here, going direct)
	LD	A,(NZSTMP+1)	;2.02
	OR	A		;2.02 Using NZTime Stamps?
	JP	NZ,NZRSTP	;2.02 ..jump if so
	CALL	P2UTIM		; Convert P-tim at (HL) to U-tim at (DE)
	LD	B,5		; Zero Last Access field for this type
GSLOOP:	LD	(DE),A		; ..by poking a zero
	INC	DE
	DJNZ	GSLOOP		; ..in each location
	CALL	P2UTIM		; Convert Modified field (P@HL to U@DE)
	JR	OKRET		; ..jump to return Ok status

;---------------------------------------------------------------;
; Convert Universal T&D to addrsd buffer in CP/M+ (P2DOS) form.	;
;								;
; Enter: DE = Address of start of Universal T&D string		;
;	 HL = Address of buffer to receive CP/M+ (P2DOS) T&D	;
; Exit :  A = 0, Zero Flag Set (Z), Time string set if Ok..	;
;	  A = FF, Zero Reset (NZ), Time string unchanged on Err	;
;	 DE --> Seconds byte in Universal field (Not moved)	;
;	 HL --> Seconds byte in CP/M+ (P2DOS) field (Not filled);
; Uses : All primary registers.					;
;---------------------------------------------------------------;

U2PTIM:	PUSH	HL		; Save destination address
	LD	A,(DE)		; Get BCD Year
	LD	B,A		; ..to B
	INC	DE		; Advance to Month
	LD	A,(DE)		; Get BCD Month
	OR	B		; Is it Invalid (YY=MM=00)?
	JR	Z,NODATE	; ..jump to error exit if Invalid stamp
	LD	A,B		; Get BCD Year again from B
	CALL	BCDBIN		; Convert year to Binary
	CP	78		; Is it 20th Century?
	JR	NC,YR19		; ..jump if so
	ADD	A,100		; Else move to 21st Century
YR19:	LD	BC,1900		; Set base century
	ADD	A,C		; Add current year to Base
	LD	C,A
	LD	A,00
	ADC	A,B
	LD	B,A
	LD	A,(DE)		; Get BCD Month
	INC	DE
	CALL	BCDBIN		; ..convert to Binary
	LD	H,A
	LD	A,(DE)		; Get Day
	INC	DE		; Point to U-Hours
	PUSH	DE		; ..and save addr on stack
	CALL	BCDBIN		; ..convert Day to Binary
	LD	L,A		; Day to L (binary)

; Check validity of day, month, year.  (CHKDAT..From DATE.ASM)
; Enter:  L = binary day
;	  H = binary month
;        BC = binary year

	LD	A,H		; Month must be..
	DEC	A		; Convert valid Month to 0-11 range
	CP	12		; Is it a valid Month?
	JR	NC,BADDAT	; ..jump error if invalid
	PUSH	HL		; Save year
	LD	E,A
	LD	D,0
	LD	HL,DM		; Set lookup table for months
	ADD	HL,DE
	LD	D,(HL)		; Get days in this month
	POP	HL
	CP	1		; Is this February? (2-1)
	CALL	Z,LEAPYR	; ..check for leap year if so
	JR	NZ,CHKDT0	; ..jump if not
	INC	D		; else make 29 days
CHKDT0:	LD	A,L		; Check for day within range
	DEC	A		; Have day > 0, check for <= max day
	CP	D
	JR	NC,BADDAT	; ..anything else is error

; Calculate 16-bit Binary Date since 1978 in Days
; Entry: BC = Year (1978..2077) (really works til 2157)
;	  H = Month (1..12)
;	  L = Days (1..31)
; Exit : DE = Days	First day (0001H) : Su 01 Jan 1978
;			Last day  (8EADH) :    31 Dec 2077
;		   Real Last day  (FFFFH) : Su 05 Jun 2157

	PUSH	HL		; Save Month (H) and Day (L)
	LD	H,0		; Null out Month leaving just days
	EX	DE,HL		; ..move to DE
	LD	L,C		; Move current Year to HL
	LD	H,B
	LD	BC,1978		; Start with base year in BC
DAYS0:	OR	A
	SBC	HL,BC		; Is this the starting year?
	ADD	HL,BC
	JR	Z,DAYS1		; ..jump if so
	PUSH	HL
	LD	HL,365		; Add days in non-leap year
	ADD	HL,DE		; ..to total days count in DE
	EX	DE,HL		; ...and put new Days total in DE
	POP	HL
	CALL	LEAPYR		; Is this a Leap year?
	INC	BC		; ..(advance to next year)
	JR	NZ,DAYS0	; ..loop if not Leap Year
	INC	DE		; Else add a day
	JR	DAYS0		; ..then loop

; Error routines.  Set destination P2Dos field to all Zeros

NODATE:	INC	DE		; Advance source ptr for same routine
	INC	DE
	DEFB	03EH		; ..fall thru to 2nd POP with LD  A,0D1H

BADDAT:	POP	DE		; Restore Universal string (--> Hrs)
	POP	HL		; Restore Destination Addr for P2DOS Date
	LD	B,4		; Fill Destination field with Nulls
BADDA1:	XOR	A
BLOOP:	LD	(HL),A
	INC	HL
	DJNZ	BLOOP		; ..loop til filled
	INC	DE		; ..Advance to Exit pointer conditions
	INC	DE
	DEC	A		; Set error Flags (A=FF, Zero Clear (NZ))
	RET

; DE=Binary Day total (Year & Day only).  Mo & Da on stack, BC=Current Year

DAYS1:	POP	HL		; Restore Month & Day
	EX	DE,HL		; Binary date to HL, Mo & Day to DE
	PUSH	HL		; ..and save Binary date
	LD	HL,DM		; Address days-of-month table
	LD	E,1
DAYS2:	LD	A,D		; Check for matching month
	CP	E
	JR	Z,DAYS4		; ..exit when match
	LD	A,(HL)		; Get days in this month
	EX	(SP),HL		; Put table on stack, Binary date to HL
	ADD	A,L		; Add this month's days to Cum Binary Date
	LD	L,A
	LD	A,00
	ADC	A,H
	LD	H,A
	LD	A,E		; Check this month
	CP	2		; ..for Feb
	CALL	Z,LEAPYR	; If so, Is it a Leap Year?
	JR	NZ,DAYS3A	; ..jump if Not Leap Year and/or Not Feb
	INC	HL		; Else bump Cum Bin Date by 29 Feb
DAYS3A:	EX	(SP),HL		; Put Cum Bin date to stack, Mo Table to HL
	INC	HL		; Point to next month
	INC	E		; Bump index counter
	JR	DAYS2		; ..and loop

DAYS4:	POP	BC		; Exit here..Put Cum Binary Date to BC
	POP	DE		; Restore Universal string (--> Hrs)
	POP	HL		; ..and Destination addr from stack
	LD	(HL),C		; Put binary date in string
	INC	HL
	LD	(HL),B
SAVEM:	INC	HL
	EX	DE,HL		; Pointers to correct regs
	LDI			; Move BCD Hours..
	LDI			; ..and BCD Minutes
	EX	DE,HL		; Restore regs for exit conditions
	XOR	A		; Set OK flags and return
	RET

;---------------------------------------------------------------;
; Convert CP/M+ (P2DOS) Time to Universal Time string		;
;								;
; Enter: HL = Points to CP/M+ (P2DOS) T&D entry			;
;	 DE = Addr of destination Universal T&D entry		;
; Exit :  A = 0, Zero Flag Set (Z) Dest Date conv if OK, else..	;
;	  A = FF, Zero Clear (NZ) Dest Nulled if Error		;
;	 HL --> Seconds byte of Source P2DOS T&D (Not moved)	;
;	 DE --> Seconds byte of Dest Universal T&D (Not filled)	;
; Uses : All primaty registers.					;
;---------------------------------------------------------------;

P2UTIM:	PUSH	DE		; Save Universal T&D address on stack
	LD	E,(HL)		; Get binary date to DE
	INC	HL
	LD	D,(HL)
	INC	HL
	EX	DE,HL		; Put Binary Day/date in HL, P2Dos ptr in DE
	LD	A,H		; Check for valid entry
	OR	L		; Is date present?
	JR	NZ,P2UTI0	; ..jump if Not Null entry
	POP	HL		; Get Universal T&D Dest addr back
	LD	B,5
	CALL	BADDA1		; ..and null the U-Time field
	EX	DE,HL		; Put ptrs in correct regs
	RET			; ..and return to caller

P2UTI0:	PUSH	DE		; Save P2D Time pointer (--> Min)
	LD	BC,1978		; Beginning year
DMJ0:	LD	DE,365		; Set days in normal year
	CALL	LEAPYR		; ..check for leap year
	JR	NZ,DMJ1		; ..jump if not
	INC	DE
DMJ1:	OR	A		; When # of days left..
	SBC	HL,DE		; ..is less than days in year..
	JR	C,DMJ2		; ..year is in HL, so exit
	JR	Z,DMJ2		; ...or her if last day of Year
	INC	BC		; Bump starting year
	JR	DMJ0		; ..and back for another try

; When get here, binary year is in BC, remaining days in HL

DMJ2:	ADD	HL,DE		; Compensate for above underflow
	LD	A,1		; Start with month # 1 (Jan)
	LD	D,0		; ..prepare for 16-bit math
	PUSH	HL		; Save days remaining
	LD	HL,DM		; ..and address month table
DMJ3:	LD	E,(HL)		; Get days in current Mo to E
	CP	2		; Is it Feb?
	CALL	Z,LEAPYR	; ..Check for leap year if Feb
	JR	NZ,DMJ4		; Jump if not leap year
	INC	E		; ..else compensate
DMJ4:	EX	(SP),HL		; Swap pointer (HL) with Days Remaining (stk)
	OR	A
	SBC	HL,DE		; Subtract days in Month from Remaining days
	JR	C,DMJ5		; ..Exit if we've gone too far
	JR	Z,DMJ5		; ...or just far enough (last day of month)
	EX	(SP),HL
	INC	HL		; Point to next month in table
	INC	A		; ..bump month counter
	JR	DMJ3		; ..and Try again

; Arrive here with Binary year on Stack Top, Relative month in A (Jan = 1),
;   Days in that month in E, and binary year in BC.

DMJ5:	ADD	HL,DE		; Compensate for underflow
	EX	(SP),HL		; ..and put back on stack
	POP	HL		; Restore Day in L
	CALL	BINBCD		; Convert Month (in A) to BCD
	LD	H,B		; ..moving Year to HL
	LD	B,A
	LD	A,L		; Convert Day
	LD	L,C
	CALL	BINBCD		; ..to BCD
	LD	C,A
	LD	DE,100		; Subtract centuries, one by one..
DMJ7A:	OR	A
	SBC	HL,DE
	JR	NC,DMJ7A	; ..until we go too far
	ADD	HL,DE		; Then correct for underflow
	LD	A,L		; Get Years (tens and ones)
	CALL	BINBCD		; ..to BCD

	POP	DE		; Restore P2D Time Pointer (--> Min)
	POP	HL		; Get Universal time string addr
	LD	(HL),A		; Store Years..
	INC	HL
	LD	(HL),B		; ..Months
	INC	HL
	LD	(HL),C		; ..Days
	CALL	SAVEM		; Store Hours and Minutes & Set flags
	EX	DE,HL		; Put U-tim exit addr in DE
	RET			; ..and finish up elsewhere

;.....
; Calculate Leap Year correction (xxxxxx00B for Leap Years)
; Enter: BC = Binary year
; Exit :  Z = 1 (set (Z)) Correction necessary
;	  Z = 0 (clear (NZ)) No correction needed

LEAPYR:	BIT	0,C		; Get lower part of date
	RET	NZ		; ..return if not Leap year
	BIT	1,C		; Test other bit
	RET			; ..and return

;.....
; Convert BCD to HEX
; Enter: A = BCD digit to be converted
; Exit : A = HEX (binary) conversion
;		 All registers preserved

BCDBIN:	OR	A
	RET	Z		; Zero is same
	PUSH	BC		; Save register
	LD	B,0		; Set counter
BCDBI0:	INC	B		; Bump counter
	SUB	1		; Count down BCD..
	DAA
	JR	NZ,BCDBI0	; ..til all gone
	LD	A,B
	POP	BC
	RET

;.....
; Convert byte in A register to two packed BCD digits.

BINBCD:	PUSH	BC		; Affect only A register
	LD	B,0FFH		; Preset counter
BINBCL:	INC	B		; Bump output count
	SUB	10
	JR	NC,BINBCL	; Loop bumping counter til no more 10s
	ADD	A,10		; ..correct for underflow
	LD	C,A		; Save low nybble here for a while
	LD	A,B		; ..and bring hi one here..
	ADD	A,A		; Move it into position
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,C		; Add in Low nybble
	POP	BC		; Restore regs
	RET

;===========================================================================
; NZTIME was developed by Joe Wright as an enhancement to P2DOS Stamps.
; This implementation was developed by Harold F. Bower.
;
; NZTIME-stamped disks are marked by a 0A1H character in the Drive position
; of the fourth entry in each logical Directory Sector.  This was done to
; provide compatibility with P2DOS stamps (21H+80H).  This fourth directory
; entry contains Create, Last Access, and Last Modified stamps for each of
; the other three files in the sector.
;
; Date and Time information is bit-mapped in compressed "Universal" format
; consisting of Year (0-99, 7 bits), Month (1-12, 4 bits), Day (1-31, 5
; bits), Hour (0-23, 5 bits) and Minute (0-59, 6 bits).  Each stamp entry
; therefore consists of 27 bits, or 81 bits per Directory Entry.  The Stamp
; entry and layout in the Fourth Directory entry is:
;
;   Address
;  |  0  |  1  |  2  |  3  |  4  |  5  |  6  |  7  |  8  |  9  |  A  |
;  +-----+-----+-----+-----+-----+-----+-----+-----+-----+-----+-----+-
;   0A1H |<---Create #1---->5/4<---Access #1--->2/1<---Modify #1---->7/
;
;	   B  |  C  |  D  |  E  |  F  |  10 |  11 |  12 |  13 |  14 |
;	 -----+-----+-----+-----+-----+-----+-----+-----+-----+-----+-
;	 /6<---Create #2--->4/3<---Access #2--->1/0<---Modify #2--->6/
;
;	   15 |  16 |  17 |  18 |  19 |  1A |  1B |  1C |  1D |  1E |  1F |
;	 -----+-----+-----+-----+-----+-----+-----+-----+-----+-----+-----+
;	 /5<---Create #3---->3/2<---Access #3--->0/7<---Modify #3--->5/
;							(Bits 4-0 Reserved)
;===========================================================================
;.....
; Convert Complete 81-bit NZTIME Stamp to Universal 15-byte format
; ENTER: HL -> Byte containing Start of NZTime Stamp sequence
;	 DE -> Beginning of 15-byte Universal Stamp sequence to receive
;	  B = # Bits to skip in first byte of NZTime input
; EXIT : HL -> Last byte of NZTime string (Input+10)
;	 DE -> Byte after last in Univ Time sequence (Input+15)
;	 - 81bit NZTime sequence converted to 15-byte Universal sequence
; NOTE: No error checking performed on values.

NZRSTP:	CALL	NZSETB		; Set B with # bits to shift on 1st byte
	LD	A,(HL)
	CALL	NZPRIM		; Set up first byte
	LD	(IPTR+1),HL	; Input ptr to stack
	LD	L,A		; Byte to be shifted in L, output byte in H
	LD	A,3
PUTNZ0:	LD	(LPCTR+1),A	; Set outer loop counter
	LD	B,7
	CALL	GBITS		; Get Year (7 bits)
	LD	B,4
	CALL	GBITS		; Get Month (4 bits)
	LD	B,5
	CALL	GBITS		; Get Day (5 bits)
	LD	B,5
	CALL	GBITS		; Get Hour (5 bits)
	LD	B,6
	CALL	GBITS		; Get Minute (6 bits)
LPCTR:	LD	A,$-$
	DEC	A		; Have we done all 3 fields?
	JR	NZ,PUTNZ0	; .loop if Not (saving new count)
	JP	OKRET		; ..and exit by setting Ok Flag

GBITS:	LD	H,0		; Clear Bit Accumulator
GBITS0:	ADD	HL,HL		; Shift B7 of L into B0 of H, H shifts left
	DEC	C		; Have we shifted last of input Byte?
	JR	NZ,GBITS1	; ..jump if Not

	PUSH	HL		; Else need a byte, save regs
IPTR:	LD	HL,$-$		; Get input ptr (set above)
	INC	HL		; .go to next byte
	LD	A,(HL)		; ..fetch
	LD	(IPTR+1),HL	; ...save updated ptr
	POP	HL		; .(restore regs
	LD	L,A		; Place new Input byte for shifting
	LD	C,8		;  and init Bits Remaining counter

GBITS1:	DJNZ	GBITS0		; ..Shift another if not done
	LD	A,H		; Else get completed output byte
	CALL	BINBCD		; .make BCD for Universal
	LD	(DE),A		; ..store in Output
	INC	DE		; ...and advance to next digit
	RET

;.....
; Convert Universal Stamp sequence to NZTim sequence, enter in DIR and Write
; ENTER: HL -> Start of 81-bit Dest NzTime Stamp sequence
;	  B = # Bits to skip in first byte
;	 DE -> 15-byte Input Universal Time Stamp sequence
; EXIT :

NZWSTP:	CALL	NZSETB		; Set number of entry in A&B (0,1,2)
	PUSH	AF		; .save for next field
	CALL	GETNZ		; Transfer Univ Create to NzTime field
	POP	AF		; Restore bit skip count
	ADD	A,3		; .offset to next field
	LD	B,A		; ..save for call
	PUSH	AF		; ...and for final field
	CALL	GETNZ		; Transfer Univ Last Access to NzTime
	POP	AF		; Restore bit skip count
	ADD	A,3		; .offset to next field
	AND	07H		; ..compensate in case last field, 3d entry
	LD	B,A		; ...save bit skip counter
	CALL	GETNZ		; Transfer Univ Modify to NzTime field
	JP	WRFCB		; ..write sector and return

;.....
; Convert a single Stamp from Univ Date/Time addressed by DE to NzTime Stamp

NZTUP:	CALL	NZSETB		; Set number of entry in A&B (0,1,2)
	ADD	A,C		; .add index (0,3,6)
	LD	B,A		; ..and store
	CP	8		; Is this > 8?
	JR	C,NZTUP0	; ..jump if Not
	LD	B,0		; Else set to Start at Bit 7
	INC	HL		; .and advance a byte
NZTUP0:	CALL	GETNZ		; Conv Univ Time & Insert in NzTime stamp
	JP	WRFCB		; ..jump to write the sector & return

;------------------------ Support Routines -------------------------------
;.....
; Set Register B to number of bits to discard in first byte of stamp
; Enter: Initial arg (offset to file in Sector Buffer) set inline in SETREC
; Exit : B = 0, 1 or 2 for respective Dir entry
; Uses : AF, B

NZSETB:	LD	A,$-$		; Get Sector Entry Pointer
	RLCA			; Rotate
	RLCA			; .B6,5
	RLCA			; ..to B1,0
	LD	B,A		; Set Counter for # bits to skip
	RET			;

;.....
; Get byte from source and shift number of bits in B.  Store Bit # in C.
; Enter:  B = # Bits to discard starting at MSB
;	  E = Input Byte
; Exit :  B = 0
;	  C = Current Bit # in MSB position (8..1)
;	  A = Input byte (shifted)
; Uses : AF,BC

NZPRIM:	PUSH	AF		; Save Byte to shift
	LD	A,8		; Set Max Bit # (8..1)
	SUB	B		; .subtract # bits to skip
	LD	C,A		; ..save in bit counter
	POP	AF		; Restore Byte to possibly shift
	INC	B		; .(prepare byte for test)
NZPRI0:	DEC	B		; Last bit to shift?
	RET	Z		; ..exit if so w/B=0
	RLCA			; Else shift source 1 bit to left
	JR	NZPRI0		; ..and try again

;.....
; Convert Time Stamp in Universal form to NZTIM stamp
; ENTER:  B = Number of Bits to skip in Byte addressed by HL
;	 HL -> Byte in NzTime to start conversion
;	 DE -> 5-byte Universal Time Source String
; EXIT : HL -> Last byte changed in NzTime Stamp (Input+3)
;	 DE -> Byte after Seconds byte (Inout+5)
;	 - NzTime Stamp Updated
; NOTE: Assumes Universal Time String is Valid

GETNZ:	LD	A,(HL)		; Get first byte
	LD	(ADDPTR+1),HL	; .(save NzTim Byte ptr)
	CALL	NZPRIM		; ..and position first byte
	LD	L,A		; Set byte to shift
	LD	B,7		;
	CALL	ADDNZ		; ..Get Years (7 bits)
	LD	B,4		;
	CALL	ADDNZ		; .Month (4 bits)
	LD	B,5		;
	CALL	ADDNZ		; ..Day (5 bits)
	LD	B,5		;
	CALL	ADDNZ		; ...Hour (5 bits)
	LD	B,6		;
	CALL	ADDNZ		; ....Minute (6 bits)
	LD	A,L		; Get working byte
	LD	HL,(ADDPTR+1)	; .restore ptr into NzTime
NZST3:	RLCA			; Rotate byte to final position
	DEC	C		; Done?
	JR	NZ,NZST3	; ..exit if so
	LD	(HL),A		; Save the final byte
	RET			; ..and return

; Support routine to accumulate specified # of Bits (Nz) from BCD digit (P2D)

ADDNZ:	LD	A,8		; Start by computing bits to fill when
	SUB	B		;  converted to binary
	LD	H,A		;   and save
	LD	A,(DE)		; Get Universal byte
	INC	DE		; .bump ptr
	CALL	BCDBIN		; Convert to Binary
ADDNZ0:	ADD	A,A		; Shift to align output bits with B7 start
	DEC	H		; More to go?
	JR	NZ,ADDNZ0	; ..loop til aligned
ADDNZ1:	RLA			; Move MSB of Source to Carry
	RL	L		; .Discard bit of output to C, New bit to LSB
	DEC	C		; Time to Write Output byte & Get another?
	JR	NZ,ADDNZ2	; ..jump if Not

	PUSH	AF		; .(save regs)
	LD	A,L		; Else Get output byte
ADDPTR:	LD	HL,$-$		; Point to Target NZTime String (set above)
	LD	(HL),A		; .save byte
	INC	HL		; ..advance to next
	LD	A,(HL)		; ...fetch
	LD	(ADDPTR+1),HL	; Save updated ptr into NZTime
	LD	L,A		; .set new target byte
	POP	AF		; .(Restore regs)
	LD	C,8		; ..and bit counter
ADDNZ2:	DJNZ	ADDNZ1		; Loop til all bits sent to output
	RET

;---------------------------------------------------------------+
; Calculate offset within T&D Record if one exists.		;
;								;
; Enter: A = Sector Pointer (0,20H,40H) into DIRBUF		;
;	BC = Points to the ZSDOS WRFCB routine			;
;	DE = Points to Directory Sector Buffer			;
;	HL = DMA Address (or flag in L for STAMPx)		;
; Exit : A = 0, Zero Flag Set (Z), If Ok, else A <> 0, (NZ)	;
;	HL = First byte of Create Date for record if OK		;
;	DE = DMA Address (or flag in L for STAMPx)		;
; Uses : HL (Entry DE), AF, BC.  Entry HL preserved in DE	;
;---------------------------------------------------------------+

SETREC:	LD	(WRFCB+1),BC	; Save Directory Write Routine address
	EX	DE,HL		; DIR sector to HL for adr calcs
	LD	BC,060H		; Offset to T&D Fields
	ADD	HL,BC
	LD	C,A		; ..Sector pointer to register
	LD	(NZSETB+1),A	;  in Subroutine
	LD	A,(HL)		; Get byte
	SUB	21H		; Is TimeStamping present?
	LD	(NZSTMP+1),A	; .(Set Flag 0 = P2Dos, 80H = NZTIM)
	AND	7FH		; Stamp Present?
	JP	NZ,NO$TD	; ..quit here if not
	LD	A,C		; Restore Sector pointer from storage
	RRCA			; Shift 2 times
	RRCA
	LD	C,A		; ..save temporarily
	RRCA			; Shift 2 more times
	RRCA
	ADD	A,C		; ..and add in again
	LD	C,A		; Set for offset (C=0,10,20)
	ADD	HL,BC		; Add offset
	INC	HL		; ..and bump to Create Time Start
	XOR	A		; Set good return status
	RET

;===================================================;
;===|		D A T A     A R E A  		|===;
;===================================================;
; Put in CSEG to make single module

;.....
; Days-in-Month table

DM:	DEFB	31,28,31,30,31,30,31,31,30,31,30,31

;.....
; Time/Date String in Universal Format

DSTIME:	DEFB	0,0,0,0,0,0

;******************************************************************
; Clock Driver for ZSDOS
;
; This routine interfaces the ZSDOS Time interface to a physical
; clock driver routine.  The ZSDOS interface is:
;
;	Entry conditions:
;		C  = Read/Write Code (1=Write, 0=Read)
;		DE = Address to Put/Set Time
;
; The Error return code on Clock Set is overwritten during instal-
; lation if a ZSDOS clock driver (with two jumps) is detected, with
; a relative jump to the clock set vector.  This short routine also
; places the time address in the HL registers to be compatible with
; DateStamper clock specifications.
;******************************************************************

	DEFS	8-[[$-BEGIN] MOD 8]	; Must start on boundary

RWCLK:	EX	DE,HL		; Set registers for DS clock interface
	LD	A,C
	OR	A		; Read (0) or Write (<>0)
	JR	Z,CLK		; Read clock if Zero..

; The following Error Return code is overwritten if ZSDOS clock added

	XOR	A		; Set error return
	DEC	A
	RET

;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
;		C l o c k     D r i v e r
;:::::::::::::::::::::::::::::::::::::::::::::::::::::::::::
; Actual clock driver or vector to external routine added here

CLK:

TOP	EQU	$

	END
