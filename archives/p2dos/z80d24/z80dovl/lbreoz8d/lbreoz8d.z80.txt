;************************************************
;
;	LBREOZ8D.Z80
;	7/25/88
;	Gene Nolan
;
;	This is an overlay file for LBREXT26 that
;	adds support for Z80DOS time-stamping.
;	It will allow LBREXT26 to carry the create
;	and alteration date of the source library
;	to the extracted file(s).
;
;	Assemble to a .HEX file and use MLOAD:
;
;	MLOAD LBRE=LBREXT26.COM,LBREOZ8D
;
;***********************************************


	org	103Ch		; LBREXT opens source file here
	call	openem

	org	106Bh		; LBREXT does writes to destination here
	call	wrtseq

	org	1017h		; LBREXT close's destination file here
	call	closem

	org	1052h		; LBREXT MAKE's the destination file here
	call	makem

	org	1A10h		; Patch above to here

openem:
	push	de
	push	bc
	call	5		; Call BDOS
	pop	bc
	pop	de
	cp	0ffh		; Open ok?
	ret	z		; Z=no
	push	de
	push	bc
	push	af
	ld	c,54		; Z80DOS call for GETSTAMP
	call	5
	pop	af
	pop	bc
	pop	de
	ret

wrtseq:				; Vector MAKE,WRITE SEQ,CLOSE to here
closem:
makem:	push	de
	push	bc
	ld	c,55		; Z80DOS call for use stamp
	call	5
	pop	bc
	pop	de
	push	bc
	push	de
	call	5		; And do requested function
	pop	de
	pop	bc
	ret
buffer:				; ACOPY's data buffer is now here, was
				; originally at 13CEH
	end