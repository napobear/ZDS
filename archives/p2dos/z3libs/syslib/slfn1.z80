;	TITLE	"SLFN1 - Syslib 4.0"
	NAME	('LFN1')
;=================================================================
;   The Libraries, Version 4, (C) 1989 by Alpha Systems Corp.
;-----------------------------------------------------------------
; Author  : Harold F. Bower
;		Derived from SLFN1.Z80 by Richard Conn
; Date    : 17 Sep 89
; Version : 1.2
; Module  : SLFN1
; Abstract: This module contains the routine LFN1 which prints
;	the Filename and Type (in FCB fixed-field format) to the
;	currently logged LST: device.  The format of the output is:
;	xxxxxxxx.xxx.  Most significant bits of all characters are
;	masked off.
; Revision:
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
; Module Entry Points

	PUBLIC		LFN1

; From SYSLIB Get..

	EXT		LOUT

	.Z80
	CSEG
;===============================================================
; NAME - LFN1
; Entry: DE - Points to first character of an FCB Filename.
; Exit : - None.  File name and type are printed to LST:
; Uses : - None
; Special Requirements: None
;===============================================================

LFN1:	PUSH	BC		; Save BC register
	PUSH	DE		; save regs
	PUSH	AF
	LD	B,8		; 8 chars first
	CALL	LFNX		; Print Name to LST: device
	LD	A,'.'
	CALL	LOUT		; Put dot to LST: device
	LD	B,3		; 3 more chars
	CALL	LFNX		; Print Type to LST: device
	POP	AF		; restore regs
	POP	DE
	POP	BC
	RET

LFNX:	LD	A,(DE)		; get char
	AND	7FH		; mask out msb
	CALL	LOUT		; print to LST: device
	INC	DE		; pt to next
	DJNZ	LFNX		; count down
	RET

	END
