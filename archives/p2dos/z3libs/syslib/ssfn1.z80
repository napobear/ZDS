;	TITLE	"SSFN1 - Syslib 4.0"
	NAME	('SFN1')
;=================================================================
;   The Libraries, Version 4, (C) 1989 by Alpha Systems Corp.
;-----------------------------------------------------------------
; Author  : Harold F. Bower
;		Derived from SFN1.Z80 by Richard Conn
; Date    : 17 Sep 89
; Version : 1.2
; Module  : SSFN1
; Abstract: This module contains the routine SFN1 which prints
;	the Filename and Type (in FCB fixed-field format) to the
;	currently logged CON:/LST: (switched) devices.  The
;	format of the output is: xxxxxxxx.xxx.  Most significant
;	bits of all characters are masked off.
; Revision:
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
; Module Entry Points

	PUBLIC		SFN1

; From SYSLIB Get..

	EXT		SOUT

	.Z80
	CSEG
;===============================================================
; NAME - SFN1
; Entry: DE = Pointer to first character of an FCB Filename.
; Exit : - No status returned, the File name and type are
;	    printed to the CON:/LST: (switched) output
; Uses : - None
; Special Requirements: None
;===============================================================

SFN1:	PUSH	BC		; Save BC register
	PUSH	DE		; save regs
	PUSH	AF
	LD	B,8		; 8 chars first
	CALL	PRFNX
	LD	A,'.'
	CALL	SOUT		; Put dot to appropriate device
	LD	B,3		; 3 more chars
	CALL	PRFNX
	POP	AF		; restore regs
	POP	DE
	POP	BC
	RET

PRFNX:	LD	A,(DE)		; get char
	AND	7FH		; mask out msb
	CALL	SOUT		; print to appropriate device
	INC	DE		; pt to next
	DJNZ	PRFNX		; count down
	RET

	END
