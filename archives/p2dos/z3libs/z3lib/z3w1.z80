	TITLE	"Z3W1 - Z3lib 4.0"
;================================================================
;   The Libraries, Version 4, (C) 1989 by Alpha Systems Corp.
;----------------------------------------------------------------
; Author  : Harold F. Bower
;		Derived from Z3W1.Z80 Ver 1.1 by Richard Conn
; Date    : 15 Mar 87
; Version : 1.2
; Module  : Z3W1
; Abstract: This module contains the routine WAIT1S which
;	pauses approximately 1 second
; Revision:
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

;  Module Entry Points

	PUBLIC		WAIT1S

;  External References

	EXT		GETSPEED

	.Z80
	CSEG
;===============================================================
; NAME - WAIT1S
; Entry: - None
; Exit : - Nothing returned.  Processor pauses approx 1 second
; Uses : - None
; Special Requirements: None
;===============================================================

WAIT1S:	PUSH	AF		;affect no regs
	PUSH	DE
	PUSH	BC
	CALL	GETSPEED	;get processor speed
	JR	NZ,W1
	LD	A,4		;assume 4MHz if none given

W1:	LD	B,A		;speed in B

LOOP0:	LD	D,50		;Outer loop = 50*20,079 = 1,003,950 cycles
LOOP1:	LD	E,218		;Inner loops = 218*92+23 = 20,079 cycles
LOOP2:	EX	(SP),HL		;19 cycles
	EX	(SP),HL		;+19 = 38 cycles
	EX	(SP),HL		;+19 = 57 cycles
	EX	(SP),HL		;+19 = 76 cycles
	DEC	E		;+ 4 = 80 cycles
	JR	NZ,LOOP2	;+12 = 82 cycles
	DEC	D		;outer loop
	JR	NZ,LOOP1
	DJNZ	LOOP0		;speed loop
	POP	BC		;restore regs
	POP	DE
	POP	AF
	RET

	END
