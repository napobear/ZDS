	TITLE	"Z33REPRS - Z3lib 4.0"
;=================================================================
;    Copyright (C) 1989 by Jay Sage.  Permission granted to
; Alpha Systems Corp. to distribute with The Libraries, Version 4
;-----------------------------------------------------------------
; Author  : Jay Sage		 modified by Harold F. Bower
; Date    :
; Version : 1.0
; Module  : Z33REPRS
; Abstract: This module contains the routine REPARSE which calls
;	the REPARSE entry point in the Command Processor to reparse
;	the Command Line starting one token later.  It is the user's
;	responsibility to be sure that the Command Processor has not
;	been overwritten in memory.  No registers are altered, and
;	the two default File Control Blocks are filled.
; Revision: 4/28/88 - set return addr by CALLing around code.  HFB
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
; Module Entry Points

	PUBLIC		REPARSE

; External References

	EXT		GETCCP, SETEPB, RESEPB

; Definitions

OFFPARSE  EQU	16		; Offset to entry point to REPARSE

	.Z80
	CSEG
;===============================================================
; NAME - REPARSE
; Entry: - None.  Some command must be in the Command Line buffer
; Exit : - None.  The default FCBs are filled
; Uses : - None.
; Special Requirements : The Command Processor must not have been
;	overwritten.
;===============================================================

REPARSE: PUSH	HL		; Preserve registers
	PUSH	DE
	PUSH	BC
	PUSH	AF
	CALL	SETEPB		; Set external program bit
	CALL	REPAR1		; Set up return addr by CALLing around

; Return here from parsing

	CALL	RESEPB		; Reset external program bit
	POP	AF		; Restore the regs
	POP	BC
	POP	DE
	POP	HL
	RET

REPAR1:	CALL	GETCCP		; Get addr of Command Processor
	LD	DE,OFFPARSE	; ..offset to parser entry point
	ADD	HL,DE
	JP	(HL)		; Call the Reparsing routine

	END
