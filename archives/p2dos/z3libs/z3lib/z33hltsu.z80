	TITLE	"Z33HLTSU - Z3lib 4.0"
;=================================================================
;    Copyright (C) 1989 by Jay Sage.  Permission granted to
; Alpha Systems Corp. to distribute with The Libraries, Version 4
;-----------------------------------------------------------------
; Author  : Jay Sage.
; Date    :
; Version : 1.0
; Module  : Z33HLTSU
; Abstract: This module contains the routine HALTSUB which stops
;	a Submit job by deleting the $$$.SUB file.  It uses the
;	submit FCB in the Command Processor so that it adapts
;	automatically to the correct drive and file Name/Type.
; Revision:
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
; Module Entry Points

	PUBLIC		HALTSUB

; External References

	EXT		GETSFCB, GUA, SUA

; Definitions

BDOS	EQU	0005H		; BDOS entry vector address
F$DEL	EQU	19		; BDOS File Delete command

	.Z80
	CSEG
;===============================================================
; NAME - HALTSUB
; Entry: - None
; Exit : - None
;	  All registers preserved/not affected
; Special Requirements : None
;===============================================================

HALTSUB: PUSH	HL		; Save primary registers
	PUSH	DE
	PUSH	BC
	PUSH	AF
	CALL	GUA		; Get current user area
	PUSH	AF		; ..and save it on stack
	XOR	A		; Log into User 0
	CALL	SUA
	CALL	GETSFCB		; Get submit FCB pointer to DE
	LD	C,F$DEL		; Set BDOS delete function number
	CALL	BDOS		; ..and delete it
	POP	AF		; Restore former user area
	CALL	SUA
	POP	AF		; Restore regs
	POP	BC
	POP	DE
	POP	HL
	RET

	END
