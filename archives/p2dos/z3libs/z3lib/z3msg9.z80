	TITLE	"Z3MSG9 - Z3lib 4.0"
;================================================================
;   The Libraries, Version 4, (C) 1989 by Alpha Systems Corp.
;----------------------------------------------------------------
; Author  : Harold F. Bower
;		Derived from Z3MSG9.Z80 Ver 1.1 by Richard Conn
; Date    : 2 May 92
; Version : 1.3
; Module  : Z3MSG9
; Abstract: This module contains the routines STOPZEX, STRTZEX
;	and PUTZEX which Suspend ZEX processing, Resume ZEX
;	processing, and Sets the ZEX message byte respectively
; Revision:
;  1.3 -  2 May 92 - Abort Setting flag if No Env/Buffer.	HFB
;  1.2 - 15 Mar 87 - Original Ver 4 Release.			HFB
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

;  Module Entry Points

	PUBLIC		STOPZEX, STRTZEX, PUTZEX

;  External References

	EXT		GETMSG, @PUT1

	.Z80
	CSEG
;===============================================================
; NAME - STOPZEX
; Entry: - None
; Exit : - None, ZEX is suspended
; Uses : - None
; Special Requirements: None
;===============================================================

STOPZEX: PUSH	AF
	LD	A,2		;suspend ZEX
	JR	STZEX0

;===============================================================
; NAME - STRTZEX
; Entry: - None
; Exit : - None, ZEX processing is resumed
; Uses : - None
; Special Requirements: None
;===============================================================

STRTZEX: PUSH	AF
	XOR	A		;A=0 means to resume ZEX processing
STZEX0:	CALL	PUTZEX
	POP	AF
	RET

;===============================================================
; NAME - PUTZEX
; Entry:  A - Contains a ZEX message byte to set
; Exit : - None, ZEX message byte is set
; Uses : - None
; Special Requirements: None
;===============================================================

PUTZEX:	PUSH	HL		;save HL
	PUSH	DE		;save DE
	PUSH	AF		;save A and Flags
	CALL	GETMSG		;get ptr to messages
	JR	Z,PUTZER	;1.3 ..exit if No Env/Msg Buffer
	LD	DE,7		;offset to message byte
	JP	@PUT1		; ..Offset and save byte

PUTZER:	POP	AF		;1.3 Restore Regs
	POP	DE		;1.3
	POP	HL		;1.3
	RET			;1.3 ..and return to caller

	END
