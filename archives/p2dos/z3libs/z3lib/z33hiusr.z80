	TITLE	"Z33HIUSR - Z3lib 4.0"
;=================================================================
;    Copyright (C) 1989 by Jay Sage.  Permission granted to
; Alpha Systems Corp. to distribute with The Libraries, Version 4
;-----------------------------------------------------------------
; Author  : Jay Sage
;		Restructured for speed on Z33 systems.  Harold F. Bower
; Date    : 24 Apr 88
; Version : 1.1
; Module  : Z33HIUSR
; Abstract: This module contains the routine HIGHUSER which tests
;	to see whether or not the Command Processor supports User
;	numbers above 15.  If ZCPR33 is not running or if High User
;	support is disabled, then HIGHUSER returns with the Zero
;	Flag Set.
; Revision:
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
; Module Entry Points

	PUBLIC		HIGHUSER

; External References

	EXT		Z33CHK, GETCCP

; Definitions

OFFOPT		EQU	6		; Offset to first option byte
OHIGHUSER	EQU	1		; High User byte offset
BHIGHUSER	EQU	7		; High User bit in Option Byte 1

	.Z80
	CSEG
;===============================================================
; NAME - HIGHUSER
; Entry: - None
; Exit :  F - Zero Flag Set (Z) if No ZCPR 3.3 or No Users > 15
;		 Zero Flag Reset (NZ) if Users > 15 permitted
;	  A - Contents destroyed
; Uses : AF
; Special Requirements: None
;===============================================================

HIGHUSER:
	CALL	Z33CHK		; Is this a ZCPR 3.3 system?
	JR	NZ,NOZ33	; ..jump if not

	PUSH	HL		; Save regs for flag check
	PUSH	DE
	CALL	GETCCP		; Get address of Command Processor
	LD	DE,OFFOPT+OHIGHUSER	; Offset to flag byte
	ADD	HL,DE
	BIT	BHIGHUSER,(HL)	; ..and check high user bit
	POP	DE		; Restore regs
	POP	HL
	RET

NOZ33:	XOR	A		; Return Z set for No Users > 15
	RET

	END
