	TITLE	"VIDD - Vlib 4.1"
	NAME	('VIDD')
;=================================================================
;  Copyright (C) 1989 by Harold F. Bower.  Permission granted to
; Alpha Systems Corp. to distribute with The Libraries, Version 4
;-----------------------------------------------------------------
; Author  : Harold F. Bower
; Date    : 9 Apr 89
; Version : 1.0
; Module  : VIDD
; Abstract:  This module contains the routines DRFULL and DRHASH
;	which print Full and Hashed graphic blocks respectively.
;	   It also contains the internal entry point @GSET0 which
;	is used by several other VLIB routines.
; Revision:
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
;  Module Entry Points

	PUBLIC		DRFULL, DRHASH, @GSET0

;  External References

	EXT		@GFB, @GHB, GRXON, GRXOFF, COUT

	.Z80
	CSEG
;===============================================================
; DRFULL - Draw a Full graphics block character
;  Entry: - None
;  Exit :  A = 0 and Zero Flag Set (Z) if Error
;	   A = FFH and Zero flag Reset (NZ) if Operation OK
;  Uses : AF
; Special Requirements: None
;===============================================================

DRFULL:	PUSH	HL		; Save regs
	LD	HL,@GFB		; Point to character
	JR	@GSET0		; ..continue below

;===============================================================
; DRHASH - Draw a Hashed graphics block character
;  Entry: - None
;  Exit :  A = 0 and Zero Flag Set (Z) if Error
;	       A <> 0, Zero flag Reset (NZ) if Operation OK
;  Uses : AF
; Special Requirements: None
;===============================================================

DRHASH:	PUSH	HL		; Save regs
	LD	HL,@GHB		; Point to the character

@GSET0:	LD	A,(HL)		; Is it valid?
	OR	A
	JR	Z,NOSTR		; ..jump w/flag if not
	CALL	GRXON		; Go to graphics mode

	LD	A,(HL)		; Get the graphics character
	CALL	COUT		; Ship it

	CALL	GRXOFF		; Turn Graphics Off
	OR	0FFH		; ..and set flag for Good
NOSTR:	POP	HL
	RET

	END
