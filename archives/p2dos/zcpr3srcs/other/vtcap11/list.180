; LIST
; 
; This subroutine will list all of the terminals presently   
; supported by the current TCAP file to the CRT. This will
; be a formatted output using the VLIB routines. You will   
; be able to list 80 terminals to the CRT on one page. 
;
; The list routine will re-enter the database in the same 
; place you left off, allowing you to run the list at any
; time without fear of loosing where you were.
;
; ENTRY	None
; EXIT	Terminals are listed

list:	push	hl		; save registers
	push	de
	push	bc
	push	af
	call	cls		; clear screen
	call	gxymsg	
	db	2,27
	db	2,'VTCAP Database List Utility',1
	db	0
	
	ld	hl,buf1		; point to index buffer

scr:	call	at		; position cursor
	db	4,1
	
plist:	ld	a,(hl)
	cp	' '		; is it a blank
	jp	z,pexit		; if so - exit

pl:	ld	bc,16
	ld	de,iname
	ldir
	inc	hl
	ld	a,(hl)
	cp	'*'
	jp	z,dsdout
	call	stndend

pcont:	push	hl
	ld	hl,iname
	call	epstr
	pop	hl
	inc	hl		; point to next character
	call	bkline		; backspace 16 times and new line
	ld	a,(line)	; get line counter
	inc	a		; increnemt line counter
	ld	(line),a	; save in buffer
	cp	20		; have we reached 20 lines
	jp	z,col2		; yes - goto column 2
	cp	40		; have we reached 40 lines
	jp	z,col3		; yes - goto column 3
	cp	60		; have we reached 60 lines
	jp	z,col4		; yes - goto column 4
	cp	80		; have we reached 80 lines
	jp	z,pmore		; yes - exit
	ld	b,16		; reload BC with the length of terminal name
	jr	plist		; loop

col2:	call	at
	db	4,21
	ld	b,16
	jp	plist

col3:		call	at
	db 	4,41
	ld	b,16
	jp	plist

col4:	call	at
	db	4,61
	ld	b,16
	jp	plist

bkline:	ld	b,16

bkln:	ld	a,bksp
	call	cout
	djnz	bkln
	ld	a,lf
	call	cout
	ret

dsdout:	call	stndout
	jp	pcont

pmore:	ld	a,0
	ld	(line),a
	call	gxymsg
	db	24,5
	db	'[more] '
	db	0
	call	cin
	call	cls
	jp	scr

pexit:	ld	a,0
	ld	(line),a
	call	gxymsg
	db	24,5
	db	'[Strike Any Key] '
	db	0
	pop	af
	pop 	bc
	pop	de
	pop	hl
	call	stndend
	call	cin
	call	cls
	call	dsply
	call	data
	jp	exopt

