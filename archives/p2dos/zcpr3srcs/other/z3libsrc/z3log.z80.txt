	TITLE	"Z3LOG - Z3lib 4.3a"
	NAME	('Z3LOG')
;================================================================
;   The Libraries, Version 4, (C) 1989 by Alpha Systems Corp.
;----------------------------------------------------------------
; Author  : Harold F. Bower
;		Derived from Z3LOG.Z80 Ver 1.1 by Richard Conn
; Date    : 19 Jan 88
; Version : 1.2
; Module  : Z3LOG
; Abstract: This module contains the routine Z3LOG which logs
;	into the Drive and User specified in a given ZCPR3 FCB
; Revision: 1.3
;	'NAME'd the module and rewrote some comments.
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

;  Module Entry Points

	PUBLIC		Z3LOG

;  External SYSLIB References

	EXT		RETUD, LOGUD		

	.Z80
	CSEG
;===============================================================
; NAME - Z3LOG
; Entry: DE - Points to a ZCPR3 FCB (Drive @ 0 (A=1), User @ 13)
; Exit : - Nothing returned
; Uses : - None
; Special Requirements: None
;===============================================================

Z3LOG:	PUSH	HL		; Save the registers
	PUSH	BC
	PUSH	AF

; Get Current Drive and User in B and C

	CALL	RETUD		; Drive A = 0
	INC	B		; Drive A = 1

; Determine Drive and User from FCB

	LD	A,(DE)		; Get Drive
	OR	A		; 0 = default
	JR	Z,LOG1		; Use default

	LD	B,A		; Selected Drive in B
LOG1:	DEC	B		; Drive A = 0

; Get Selected User in C

	LD	HL,13		; Point to S1 for User
	ADD	HL,DE
	LD	C,(HL)		; Get User in C

; Log Into Disk in B, User in C

	CALL	LOGUD		; Do the work in SYSLIB module

	POP	AF		; Restore registers and return
	POP	BC
	POP	HL
	RET

	END
