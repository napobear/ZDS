	TITLE	"Z3GFNX - Z3lib 4.0"
;================================================================
;   The Libraries, Version 4, (C) 1989 by Alpha Systems Corp.
;----------------------------------------------------------------
; Author  : Harold F. Bower
;		Derived from Z3GFNX.Z80 Ver 1.1 by Richard Conn
; Date    : 21 Jan 88
; Version : 1.2
; Module  : Z3GFNX
; Abstract: This module contains the routine GETFNX which
;	returns a pointer to a specified FN.FT entry
; Revision:
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

;  Module Entry Points

	PUBLIC		GETFNX

;  External References

	EXT		ENVPTR

	.Z80
	CSEG
;===============================================================
; NAME - GETFNX
; Entry:  A - Contains the number of entry (1-4) to return
; Exit : HL - Points to 'A'th FN.FT entry
;	  A <> 0 and Zero Flag Reset (NZ) if Ok
;	       A = 0 and Zero Flag Set (Z) if Error
; Uses : AF,HL
; Special Requirements: None
;===============================================================

GETFNX:	DEC	A		;check range
	CP	4		;0 to 3 is valid
	JR	NC,ERROR
	INC	A		;set offset to 1-4 again
	PUSH	DE
	LD	HL,(ENVPTR)	;pt to environment
	LD	DE,52H		;pt to file name entry
	ADD	HL,DE
	LD	DE,11		;offset to next name

GFNX1:	DEC	A		;count down
	JR	Z,GFNX2
	ADD	HL,DE		;pt to next
	JR	GFNX1

GFNX2:	POP	DE		;restore regs
	DEC	A		;set NZ for OK
	RET

ERROR:	XOR	A		;error
	RET

	END
