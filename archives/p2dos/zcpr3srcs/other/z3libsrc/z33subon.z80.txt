	TITLE	"Z33SUBON - Z3lib 4.0"
;=================================================================
;    Copyright (C) 1989 by Jay Sage.  Permission granted to
; Alpha Systems Corp. to distribute with The Libraries, Version 4
;-----------------------------------------------------------------
; Author  : Jay Sage		 modified by Harold F. Bower
; Date    : 24 Apr 88
; Version : 1.1
; Module  : Z33SUBON
; Abstract: This module contains the routine SUBON which tests to
;	see if SUBMIT processing is enabled in the Command Processor.
;	If so, it returns NZ.  It will return Z if SUBMIT is not
;	enabled, or if the ZCPR 3.3 Command Processor cannot be
;	detected.
; Revision: Restructured order for speed on Z33 system. HFB
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
; Module Entry Points

	PUBLIC		SUBON

; External References

	EXT		Z33CHK, GETCCP

; Definitions

OFFOPT	EQU	6		; Offset to first option byte
OSUBON	EQU	3		; Offset byte for SUBON flag
BSUBON	EQU	0		; Bit in OSUBON flag for proc enabled

	.Z80
	CSEG
;===============================================================
; NAME - SUBON
; Entry: - None
; Exit :  F - Zero Flag Clear (NZ) if SUBMIT processing enabled, else
;		 Zero Set (Z) if No ZCPR 3.3 or SUBMIT disabled
;	  A - destroyed
; Uses : AF
; Special Requirements : Command Processor must be in memory
;===============================================================

SUBON:	CALL	Z33CHK		; Is ZCPR 3.3 present?
	JR	NZ,SUBON1	; ..jump if Not
				; ZCPR33 found, so continue
	PUSH	HL		; Save registers
	PUSH	DE
	CALL	GETCCP		; Get address of CCP
	LD	DE,OFFOPT+OSUBON	; Add offset to subon option bit
	ADD	HL,DE
	BIT	BSUBON,(HL)	; Test the SUBON bit
	POP	DE		; Restore regs
	POP	HL
	RET

SUBON1:	XOR	A		; Clear register & Set Z if Z33 not found
	RET


	END
