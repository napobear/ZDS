;
; DOSVER - a quick program which displays the version number of the
; currently-running ZRDOS.  The original DOSVER did not display the
; version number correctly in Z-System implementations such as the DT42
; "hyperspace" system (which was v2.0).
;
; Released by: D. McCord, 25 June 1987
;

vers			equ	02	; version of this program
dos			equ	5	; dos entry point
return$zrdos$ver	equ	48	; dos function #

	extrn	pafdc,print		; SYSLIB functions

start:
	ld	c,return$zrdos$ver
	call	dos			; get dos ver #
	ld	a,l
	or	h			; is it 0?
	jr	z,not$zrdos
	ex	de,hl			; save version #
	call	print
	db	'ZRDOS version ',0
	ld	a,e
	and	0f0h			; strip low nybble
	rrc	a			; move to proper position
	rrc	a
	rrc	a
	rrc	a
	call	pafdc			; print first digit
	call	print
	db	'.',0			; period
	ld	a,e
	and	0fh			; strip high nybble
	call	pafdc			; print second digit
	call	print
	db	' is currently running',0
	ret				; exit program

not$zrdos:
	call	print
	db	'ZRDOS is not currently running',0
	ret				; exit program
