	TITLE	"SCOUT - Syslib 4.3"
	NAME	('COUT')
;=================================================================
;   The Libraries, Version 4, (C) 1989 by Alpha Systems Corp.
;-----------------------------------------------------------------
; Author  : Harold F. Bower
;		Derived from SCOUT.Z80 by Richard Conn
; Date    : 28 Oct 89
; Version : 1.3
; Module  : SCOUT
; Abstract: This module contains the routine COUT which prints the
;	character in register A to the currently logged CON: device
;	with a BIOS call.
; Revision:
;  1.4 - 17 Sept 90 - Changed COUT and COUT7 to return AF intact.  JWW
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
; Module Entry Points

	PUBLIC		COUT, COUT7

; From SYSLIB Get..

	EXT		@BIOSA

	.Z80
	CSEG
;===============================================================
; NAME - COUT7
; Entry:  A - Contains 7 or 8 bit character to output to CON:
; Exit :  A - Char with MSB Reset.  Char passed to BIOS ConOut
; Uses : - AF
; Special Requirements: The BIOS MUST start on even page boundary.
;===============================================================

COUT7:	PUSH	AF
	AND	7FH		; Strip MSB
	JR	COUT0		; ..jump around COUT's PUSH AF

;===============================================================
; NAME - COUT
; Entry:  A - Contains the character to output to CON:
; Exit : - Nothing returned.  Char passed to BIOS ConOut routine
; Uses : - None
; Special Requirements: The BIOS MUST start on even page boundary.
;===============================================================

COUT:	PUSH	AF
COUT0:	PUSH	BC		; Save regs not preserved in @BIOSA
	LD	C,A		; Put char in C
	LD	A,4		; ..and set Print function #
	CALL	@BIOSA		; Do the work
	POP	BC		; Restore the regs
	POP	AF
	RET

	END
