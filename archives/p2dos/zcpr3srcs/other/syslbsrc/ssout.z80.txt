;	TITLE	"SSOUT - Syslib 4.0"
	NAME	('SOUT')
;=================================================================
;   The Libraries, Version 4, (C) 1989 by Alpha Systems Corp.
;-----------------------------------------------------------------
; Author  : Harold F. Bower
;		Derived from SSOUT.Z80 Ver 1.1 by Richard Conn
; Date    : 2 May 92
; Version : 1.4
; Module  : SSOUT
; Abstract:  This module contains the routine SOUT which sends
;	a character to the CON: and/or LST: device depending on
;	the value stored in the public variable SCTLFL.
; Revision:
;  1.4 -  2 May 92 - Mod to Preserve Entry Flags.		HFB
;  1.3 - 11 Jun 89 - Original Version 4 Release.		HFB
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
; Module Entry Points

	PUBLIC		SOUT

; External References

	EXT		SCTLFL, COUT, LOUT

	.Z80
	CSEG
;===============================================================
; NAME - SOUT
; Entry:  A - Contains the character to print
; Exit : - Nothing returned, the character is printed
; Uses : - None
; Special Requirements: None
;===============================================================

SOUT:	PUSH	AF		;1.4 Preserve Flags
	PUSH	HL		; Save HL Reg
	LD	HL,SCTLFL	; Get control flag
	BIT	7,(HL)		; Send to LST:?
	CALL	NZ,LOUT
	BIT	0,(HL)		; Send to CON:?
	CALL	NZ,COUT
	POP	HL		; Restore HL
	POP	AF		;1.4 ..and Flags
	RET

	END
