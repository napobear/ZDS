;	TITLE	"SCFN1 - Syslib 4.0"
	NAME	('PFN1')
;=================================================================
;   The Libraries, Version 4, (C) 1989 by Alpha Systems Corp.
;-----------------------------------------------------------------
; Author  : Harold F. Bower
;		Derived from SPFN1.Z80 by Richard Conn
; Date    : 17 Sep 89
; Version : 1.2
; Module  : SCFN1
; Abstract: This module contains the routine PFN1 which prints
;	the Filename and Type (in FCB fixed-field format) to the
;	currently logged CON: device.  The format of the output is:
;	xxxxxxxx.xxx.  Most significant bits of all characters are
;	masked off.
; Revision:
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
; Module Entry Point

	PUBLIC		PFN1

; From SYSLIB Get..

	EXT		COUT7

	.Z80
	CSEG
;===============================================================
; NAME - PFN1
; Entry: DE = Pointer to the first character of an FCB Filename.
; Exit : - Nothing returned.  Name & type printed on CON: masking MSBs
; Uses : - None
; Special Requirements: None
;===============================================================

PFN1:	PUSH	BC		; Save BC register
	PUSH	DE		; Save regs
	PUSH	AF
	LD	B,8		; 8 chars first
PFN1A:	LD	A,(DE)		; Get char
	CALL	COUT7		; Print to CON: masking MSBs
	INC	DE		; .pt to next
	DJNZ	PFN1A		; ..count down
	LD	A,'.'
	CALL	COUT7		; Put dot to CON:
	LD	B,3		; 3 more chars
PFN1B:	LD	A,(DE)		; Get char
	CALL	COUT7		; Print to CON: masking MSBs
	INC	DE		; .pt to next
	DJNZ	PFN1B		; ..count down
	POP	AF		; Restore regs
	POP	DE
	POP	BC
	RET

	END
