;	TITLE	"SCAPSTR - Syslib 4.0"
	NAME	('CAPSTR')
;=================================================================
;   The Libraries, Version 4, (C) 1989 by Alpha Systems Corp.
;-----------------------------------------------------------------
; Author  : Harold F. Bower
;		Derived from SCAPSTR.Z80 Ver 1.1 by Richard Conn
; Date    : 17 Sep 89
; Version : 1.2
; Module  : SCAPSTR
; Abstract:  This module contains the routine CAPSTR which
;	capitalizes all characters in a Null-terminated character
;	string.
; Revision:
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
; Module Entry Points

	PUBLIC		CAPSTR

; From SYSLIB Get

	EXT		CAPS

	.Z80
	CSEG
;===============================================================
; NAME - CAPSTR
; Entry: HL - Contains the address of the character string
; Exit : - Nothing returned, the string is capitalized
; Uses : - None
; Special Requirements: None
;===============================================================

CAPSTR:	PUSH	AF		; Save regs
	PUSH	HL
CAPSLP:	LD	A,(HL)		; Get char
	OR	A		; Done?
	JR	Z,CAPSDN
	CALL	CAPS		; Capitalize it
	LD	(HL),A		; Put it back
	INC	HL		; Pt to next
	JR	CAPSLP

CAPSDN:	POP	HL		; Get regs
	POP	AF
	RET

	END
