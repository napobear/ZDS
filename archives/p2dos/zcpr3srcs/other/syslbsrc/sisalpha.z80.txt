;	TITLE	"SISALPHA - Syslib 4.0"
	NAME	('ISALPH')
;=================================================================
;   The Libraries, Version 4, (C) 1989 by Alpha Systems Corp.
;-----------------------------------------------------------------
; Author  : Harold F. Bower
;		Derived from SISALPHA.Z80 Ver 1.1 by Richard Conn
; Date    : 28 Jul 89
; Version : 1.3
; Module  : SISALPHA
; Abstract: This module contains the routine ISALPHA which tests the
;	character in the A register to determine if it is an ASCII
;	alphabetic character.  If it is, the Zero flag is set (Z), else
;	the Zero flag is Reset (NZ).  The character is unaffected.
; Revision:
;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
; Module Entry Points:

	PUBLIC		ISALPHA

; From SYSLIB Get..

	EXT		CAPS

	.Z80
	CSEG
;=========================================================================
; NAME - ISALPHA
; Entry: A = Character to evaluate
; Exit : A = Character, Zero Flag set (Z) if ASCII alphabetic
;		 Zero Flag Reset (NZ) if NOT
; Uses : F	  <HL>,<DE>,<BC>,<A> - Preserved
; Special Requirements:  None
;=========================================================================
ISALPHA: PUSH	BC		; Save BC
	LD	C,A		; Save char in C
	AND	7FH		; Mask out MSB
	CALL	CAPS		; Capitalize
	CP	'A'		; Less than A?
	JR	C,NO
	CP	'Z'+1		; Less than or equal to Z?
	JR	C,YES
NO:	DEFB	0F6H		; Clear Z flag with "OR  0AFH"
YES:	XOR	A		; Set Z flag for Ok
	LD	A,C		; Get char
	POP	BC		; Restore BC
	RET

	END
