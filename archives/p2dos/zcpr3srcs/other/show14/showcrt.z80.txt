; This module shows data from the environments, including the characteristics
; of the console and list devices.

; SHOWCRT.Z80

cpdata:
	call	banner
	call	gxymsg
	defb	3,29
	defb	highon,' Data from Environment ',highoff,0

; CRT information

	call	gxymsg
	defb	6,20
	defb	highon
	defb	' I/O DEVICE:      LST           CON '
	defb	highoff,0

	call	gxymsg
	defb	7,20
	defb	highon
	defb	' width       '
	defb	highoff,0

	call	gxymsg
	defb	8,20
	defb	highon
	defb	' total lines '
	defb	highoff,0

	call	gxymsg
	defb	9,20
	defb	highon
	defb	' text lines  '
	defb	highoff,0

	call	gxymsg
	defb	10,20
	defb	highon
	defb	' formfeed    '
	defb	highoff,0

	ld	hl,7*100h+38	; Start display at row 7, column 38
	ld	a,37h		; Print data at offset 37H in ENV
	call	lprt

	ld	hl,7*100h+52
	ld	a,31h		; Print data at offset 31H in ENV
	call	cprt

; Miscellaneous environment data

	call	gxymsg
	defb	14,10
	defb	highon,' DU OK: ',highoff,' ',0

	ld	a,2eh
	call	compenv
	ld	a,(hl)
	call	pryna

	call	vprint
	defb	'   ',highon,' Max DU: ',highoff,' ',0
	ld	a,2ch
	call	compenv
	ld	a,(hl)
	add	a,'@'
	call	cout
	inc	hl
	ld	a,(hl)
	call	pafdc

	call	vprint
	defb	'   ',highon,' Speed: ',highoff,' ',0
	ld	a,2bh
	call	compenv		; Set up pointer to data
	ld	a,(hl)
	call	pafdc
	call	vprint
	defb	' MHz',0

	call	vprint
	defb	'   ',highon,' Quiet: ',highoff,' ',0
	ld	a,28h
	call	compenv
	ld	a,(hl)
	call	pryna

	call	gxymsg
	defb	17,25
	defb	highon,' Drive Map: ',highoff,'  ',0

	ld	a,34h		; Offset into ENV
	call	compenv
	ld	e,(hl)		; Load drive map vector into DE
	inc	hl
	ld	d,(hl)
	ex	de,hl		; Now into HL
	ld	d,'A'		; Drive letter when in map
	ld	e,'-'		; Dash when not in map
	ld	b,16		; Number of drives
maploop:
	ld	a,e		; Prepare to display dash
	rr	h		; Shift bits in HL
	rr	l
	jr	nc,maploop1	; Skip if drive bit not set
	ld	a,d		; Get drive letter
maploop1:
	call	cout		; Display the character
	inc	d		; Advance to next drive letter
	djnz	maploop

	jp	getcmd

;----------------------------------------

; Subroutine to display CRT data

cprt:
	ld	(infoxy),hl
	call	compenv		; Compute offset

	call	dispxy
	ld	a,(hl)
	call	padc

	call	dispxy
	inc	hl
	ld	a,(hl)
	call	padc

	call	dispxy
	inc	hl
	ld	a,(hl)
	call	padc
	ret

;----------------------------------------

; Print LST Data

lprt:
	ld	(infoxy),hl
	call	compenv		; Compute offset

	call	dispxy
	ld	a,(hl)
	call	padc

	call	dispxy
	inc	hl
	ld	a,(hl)
	call	padc

	call	dispxy
	inc	hl
	ld	a,(hl)
	call	padc

	call	dispxy
	inc	hl
	ld	a,(hl)
	call	pryna
	ret

; End SHOWCRT.Z80

